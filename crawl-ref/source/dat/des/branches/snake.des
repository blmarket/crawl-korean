###############################################################################
# snake.des: All maps and branch endings relating to the Snake Pit.
###############################################################################

##############################################################################
# Snake Pit entry vaults
##############################################################################

NAME:   dummy_snake_entry
TAGS:   snake_entry
KFEAT:  O = enter_snake_pit
MAP
O
ENDMAP

NAME:   minmay_snake_entry_guardpost_small
TAGS:   snake_entry
ORIENT: float
MONS:   naga, naga / nothing w:3
KFEAT:  O = enter_snake_pit
MAP
ccc.@.ccc
c.......c
c.2...2.c
.........
@...O...@
.........
c.1...2.c
c.......c
ccc.@.ccc
ENDMAP

NAME:   minmay_snake_entry_guardpost_medium
TAGS:   snake_entry
WEIGHT: 5
ORIENT: float
MONS:   naga, naga / nothing
KFEAT:  O = enter_snake_pit
MAP
   ccccccc
 ccc.....ccc
cc2.......2cc
c...c2.1c...c
@...c.O.c...@
c...c1.2c...c
cc2.......2cc
 ccc.....ccc
   ccccccc
ENDMAP

NAME:   minmay_snake_entry_guardpost_large
TAGS:   snake_entry
WEIGHT: 2
ORIENT: float
MONS:   naga warrior, naga / nothing
KFEAT:  O = enter_snake_pit
MAP
c@ccccccc
c.......cc
c..c.c..2cc
c.........cc
c..c.c.c..2cc
c......2....cc
cc2..c1O2c..2cc
 cc....2......c
  cc2..c.c.c..c
   cc.........c
    cc2..c.c..c
     cc.......c
      ccccccc@c
ENDMAP

NAME:   minmay_snake_entry_six_corridors
TAGS:   snake_entry ruin_lair
ORIENT: float
MONS:   ball python, adder, water moccasin, black mamba
KMONS:  P = plant / fungus w:1 / nothing
SUBST:  1 = 1., 2 = 2., 3 = 3.
SUBST:  1 = 122223333, 2 = 122333344, 3 = 33444
KFEAT:  O = enter_snake_pit
MAP
xxxx.@1xxxxxxP@xxxxx
xxxx1.PxxxxxP..xxxxx
xxxxx..xxxxx1.xxxxxx
xxxxxP..xxxx..xxxxxx
xxxxxP.1xxxx2..xxxxx
xxxxxx..Pxxxx1Pxxxxx
xxxxxxx2.xxxP.xxxxxx
xxxxxxxP1xx1..xxxxxx
xxxxxxxx...3.xxxxxxx
xxxxxxxP1..P1.xxxxxx
xx21xxP..PO.P..2xx1P
P.....1.3.1.xx.....@
@.1PxxxP..xxxx.Px1P.
xxxxxxxx.1xxxx2xxxxx
xxxxxxxx..Pxx..xxxxx
xxxxxxxx..PxxP.1xxxx
xxxxxxx1.2xxxx..xxxx
xxxxxxxP.xxxxxx1..Px
xxxxxxx.1xxxxxxxP...
xxxxxx.@.Pxxxxxxxx.@
ENDMAP

NAME:   minmay_snake_entry_spellcaster
TAGS:   snake_entry
ORIENT: float
WEIGHT: 2
MONS:   naga mage / guardian serpent
KFEAT:  O = enter_snake_pit
MAP
...
.O1
...
ENDMAP

##############################################################################
# Snake Pit specific non-ending non-entry vaults
##############################################################################

NAME:   minmay_snake_pond
DEPTH:  Snake
MONS:   ball python w:3 / adder / water moccasin
SUBST:  w = wwWW.1
MAP
  wwww
 wwwwww
wwwwwwww
wwwwwwww
wwwwwwww
wwwwwwww
 wwwwww
  wwww
ENDMAP

NAME:   minmay_snake_river
TAGS:   no_monster_gen generate_awake
DEPTH:  Snake
ORIENT: south
# Black mambas can't swim, so none of those
MONS:   ball python w:3 / adder / water moccasin / nothing w:5
MONS:   anaconda / nothing w:5
SUBST:  w = wwW
KPROP:  12 = no_rtele_into
MAP
xxxxxxxxxxxxxxxxxxxxxx..@..xxxxxxxxxxxxxxxxxxxx..@.xxxxxxxxxxxxxxxxxxx
xx211xxxxxxxxxxxxxxwwwWWWWWwwwwxxxxxxxxxxxxxwwwWWWWwwxxxxxxxxxxx112xxx
xx211wwwwxxxxxwwwwwwwwwxxxxxwwwwwwwwxxxxwwwwwwxxxxwwwwwwxxxxxxww112xxx
xx211xwwwwwwwwwwwwxxxxxxxxxxxxxwwwwwwwwwwwwxxxxxxxxxxxwwwwwwwwwx112xxx
xxxxxxxxxxwwwwwxxxxxxxxxxxxxxxxxxxwwwwwwwxxxxxxxxxxxxxxxxwwwwxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:   hangedman_venom_bottle
TAGS:   patrolling
DEPTH:  Snake
WEIGHT: 5
KMONS:  123 = water moccasin / black mamba / nothing
KMONS:  4 = guardian serpent
KMONS:  5 = naga mage / nothing
KITEM:  1235 = potion of poison / potion of strong poison / \
               potion of confusion / nothing w:20
KITEM:  4 = any weapon good_item ego:venom q:1 / \
            any weapon good_item ego:venom q:2
KFEAT:  1234 = shallow_water
KMASK:  123 = no_monster_gen
COLOUR: 1234 = lightgreen
SUBST:  1 = 1 2:1 3:1
MARKER: 2 = lua:fog_machine { cloud_type = "poison gas", pow_min = 5, \
                pow_max = 30, delay = 30, size = 2, walk_dist = 2 }
MARKER: 3 = lua:fog_machine { cloud_type = "noxious fumes", pow_min = 5, \
                pow_max = 20, delay = 40, size = 3 }
MAP
     ...
    .....
   ..+++..
  ..m+2+m..
  ..mm1mm..
 ..mm111mm..
..mm11111mm..
..mm11111mm..
..mm11411mm..
..mm11111mm..
..mmm111mmm..
 ..mmmmmmm..
  ....5....
   .......
ENDMAP

NAME:   nicolae_snake_lava_pool
DEPTH:  Snake
TAGS:   mini_float
SUBST:  L = l1
SUBST:  1 = 1.
MONS:   adder / ball python / black mamba / anaconda w:5 / \
        water moccasin / nothing w:40
KMONS:  l = lava snake / nothing w:50
KFEAT:  l = lava
MAP
    .......
  ...........
 .............
 .....111.....
.....1LLL1.....
....1LlllL1....
...1LlllllL1...
...1LlllllL1...
...1LlllllL1...
....1LlllL1....
.....1LLL1.....
 .....111.....
 .............
  ...........
    .......
ENDMAP

NAME:   nicolae_snake_springs
DEPTH:  Snake
TAGS:   mini_float
SUBST:  W = w1
SUBST:  1 = 1.
MONS:   adder / ball python / black mamba / anaconda w:5 / \
        water moccasin w:15
KMONS:  w = sea snake / nothing w:90
KFEAT:  w = deep_water
MAP
    .......
  ...........
 .............
 .....111.....
.....1WWW1.....
....1WwwwW1....
...1WwwwwwW1...
...1WwwwwwW1...
...1WwwwwwW1...
....1WwwwW1....
.....1WWW1.....
 .....111.....
 .............
  ...........
    .......
ENDMAP

NAME:   nicolae_sea_snake_s
MONS:   naga mage, naga
ORIENT: float
DEPTH:  Snake:2-4
TAGS:   no_monster_gen
SHUFFLE: {[(
SHUFFLE: (<
KFEAT:  q = w
KMONS:  q = w:15 sea snake / w:5 nothing
MAP
xxxwwwwwwwxxxxxx
@..wwwqwww.....xx
@...wwwww.......xx
@....wqw.........x
x..2.www....x....x
xx...wqw...xxx...x
 x...www...xxx...x
 x.2.wqw...xxx...x
 x...www...xxx...x
 x...wqw...xxx...x
 x.2.www...xxx...x
 x....w....xxx...x
 x.........xxx{.[x
 xx...1...xxxx...x
  xx.....xx  xx(xx
   xxxxxxx    xxx
ENDMAP

NAME:   nicolae_snake_horde_hoard
DEPTH:  Snake
KITEM:  s = $ / nothing w:25 / * w:1
KMONS:  s = adder w:5 / ball python w:5 / black mamba / anaconda / \
           water moccasin / sea snake w:1 / nothing w:20
MARKER: + = lua:restrict_door()
MAP
 xxxxxxx
xxsssssxxxxxx
xssssssss+..@
xssssssss+.0@
xssssssss+..@
xxsssssxxxxxx
 xxxxxxx
ENDMAP

NAME:   nicolae_cobra_stairs
DEPTH:  Snake:3-4
ORIENT: float
TAGS:   no_pool_fixup patrolling
SHUFFLE: }])
SUBST:  ) = >>)
MONS:   guardian serpent, greater naga, naga mage, naga warrior
MAP
       xxxxxxxx
     xxx......xx
   xxx..4.www.2xx
xxxx.9....w3w...x
@.....wwwwwWw...xx
@..0.ww.....1...}x
@..0.W.4.4.3.2..]x
@..0.ww.....1...)x
@.....wwwwwWw...xx
xxxx.9....w3w...x
   xxx..4.www.2xx
     xxx......xx
       xxxxxxxx
ENDMAP

NAME:   nicolae_snake_hallway
DEPTH:  Snake
ORIENT: float
SUBST:  A : @xx
SUBST:  B : @xx
SUBST:  C : @xx
SUBST:  T : TTTVVUY
MONS:   naga, naga warrior
MONS:   naga mage w:25 / greater naga w:5
MAP
             xxxxxx
           xxx.G.GxxxxxAAxx
xxx      xxx.G..1..Gxx.1..xxx
@Gxxx  xxx.G....1....G...T..xx
@..Gxxxx.G.2...G.G.....3...1.C
@..1.G.G.3.2.Gxxxx.G...3...1.C
@G.1...2.3.Gxxx  xxx.G...T..xx
xx.G...2.Gxxx      xxx.1..xxx
 xxx.G.Gxxx          xxBBxx
   xxxxxx
ENDMAP

# The disgraced guardian serpent Aizul has been
# demoted to watching the stairs.
NAME:   nicolae_aizul_guard
DEPTH:  Snake:2-4
TAGS:   no_rotate no_hmirror no_vmirror
ORIENT: float
SHUFFLE: ]})>
SUBST:  ) = )>
: if you.absdepth() > 14 then
KMONS:  A = patrolling Aizul, patrolling greater naga
KMONS:  B = guardian serpent / greater naga
: else
KMONS:  A = patrolling Aizul, patrolling guardian serpent
KMONS:  B = naga mage / guardian serpent
: end
NSUBST: B = A / B
MAP
 xxx.@.xxx
xx.B...B.xx
x.........x
xB.......Bx
....].>....
@.........@
....).}....
xB.......Bx
x.........x
xx.B...B.xx
 xxx.@.xxx
ENDMAP

NAME:   nicolae_snake_shapes
TAGS:   no_pool_fixup mini_float
DEPTH:  Snake
NSUBST: 0 = 2:9 / 1:8 / *:0
SUBST:  x : xcvmlwWb
MAP
      .....
   ...........
 ......xxx......
 .......0x.......
...xxx..xx..xxx...
....0x..x0...0x...
....xx..xxx..xx....
 ...x0...0x..x0....
 ...xxx..xx..xxx...
  .......x0.......
   ......xxx......
     ...........
        .....
ENDMAP

NAME:   nicolae_coiled_stairwell_down
DEPTH:  Snake:1-4
TAGS:   uniq_coiled_stairwell
SUBST:  } = }])>
MAP
  xxxxxxx
 xx.....xx
xx.xxxxx.xx
x.xx...xx.x
x.x.xxx.x.x
x.x.x}x.x.x
x.xx..x.x.x
xx.xxxx.x.x
 xx....xx.x
  xxxxxx.xx
    x...xx
   xx.xxx
   x.0.x
   x...x
   xx@xx
ENDMAP

NAME:   nicolae_coiled_stairwell_up
DEPTH:  Snake:2-5
TAGS:   uniq_coiled_stairwell
SUBST:  { = {[(<
MAP
  xxxxxxx
 xx.....xx
xx.xxxxx.xx
x.xx...xx.x
x.x.xxx.x.x
x.x.x{x.x.x
x.xx..x.x.x
xx.xxxx.x.x
 xx....xx.x
  xxxxxx.xx
    x...xx
   xx.xxx
   x.0.x
   x...x
   xx@xx
ENDMAP

################################################################################
# Snake endings
################################################################################

################################################################################
# Snake Pit: the hunt
#
NAME:    snake_hunt
PLACE:   Snake:$
TAGS:    no_rotate
ORIENT:  northwest
KFEAT:   Z = weight:2 teleport trap / .
KFEAT:   z = teleport trap / Zot trap / weight:20 .
MONS:    naga / nothing
MONS:    greater naga / nothing
MONS:    guardian serpent / nothing
MONS:    naga warrior / nothing
MONS:    naga mage / nothing
MONS:    greater naga / guardian serpent
SHUFFLE: 'X / "Y
SHUFFLE: 'X / 'X / AB, "Y / CD
SUBST:   A=x, B=x, C=x, D=x
SUBST:   ' = .:250 1 2 3 4:15 5:5
SUBST:   " = .:250 1 2 3 4:5  5:15
SUBST:   X=xc..++, Y=xc++
SUBST:   T : T U V .:30 b
SUBST:   _:.!, !:!.x, !=x .:2
SHUFFLE: 123, 45, ([<
KITEM:   O = serpentine rune of Zot
#
validate {{ return glyphs_connected('{', 'O') }}
#
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxcccccxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxx''''''c4O4c""""""xxxxxxxxxxx
xxxxxxxxxxx''''''xxxx664z466xxxx""""""xxxxxx
xxxxxxxx''''xxxxxxxx.5665665.xxxxxxxx""""xxx
xxxxxxx''xxxx''''xxx..45554..xxx""""xxxx""xx
xxxxxx''xxx''''''''xxx.666.xxx""""""""xxx""x
xxxxxx'xxx''''''''''xxxcXcxxx""""""""""xxx"x
xxxxxx''x''''''''''''xx.3.xx""""""""""""x""x
xxxxxxx'Y'''''''''''''45354"""""""""""""Y"xx
xxxxxxxxxx''''''''''xx.....xx""""""""""xxxxx
xxxxxxxxxxx''''''''xxxxx.xxxxx""""""""xxxxxx
xxxxxxxxxxxxx''''xxxxxxx.xxxxxxx""""xxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxx+xxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxx!.!xxxxxx.....xxxxxx!.!xxxxxxxx
xxxxxxxxxx!....._xx...43335...xx_...!!!xxxxx
xxxxxxxx!......_x.11.4..3..5.22.x_......!xxx
xxxxxxx!...2...x..11.........22..x...1...!xx
xxxxxxx!..2..._x......T...T......x_...1..!xx
xxxxxx!...2...x..2.............1..x...1...!x
xxxxxx!..2....x.........{.........x....1...!
xxxxxx!.......x....T...(.[...T.............@
xxxxxx!..2....x.......ZZZZZ.......x....1...!
xxxxxxx!..2..._x.................x_...1..!xx
xxxxxxx!...2..._xx....T.}.T....xx_...1...!xx
xxxxxxx!........._xx.........xx_.........!xx
xxxxxxxx!.........._xxx...xxx_..........!xxx
xxxxxxxxx!.............x.x.............!xxxx
xxxxxxxxxx!!.........................!!xxxxx
xxxxxxxxxxxx!!!.....3.......3.....!!!xxxxxxx
xxxxxxxxxxxxxxx!!!!!..3...3..!!!!!xxxxxxxxxx
xxxxxxxxxxxxxxxxxxxx!!!...!!!xxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxx!@!xxxxxxxxxxxxxxxxxx
ENDMAP

################################################################################
# Snake Pit: old level
#
NAME:   snake_pit
PLACE:  Snake:$
ORIENT: southwest
TAGS:   no_rotate
MONS:   greater naga / nothing
MONS:   naga
MONS:   naga mage / nothing
MONS:   naga warrior / nothing
SUBST:  c = c .:1
SUBST:  v = v .:1
SUBST:  v = x
KITEM:  O = serpentine rune of Zot
MAP
xxxxxxxxxxxxxxxxxxxxxxx..@.xxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxx.............xxxxxxxx
xxxxxxxxxxxxxx....v.............v..xxxxx
xxxxxxxxxxxx....2.v.............v.2..xxx
xxxxxxxxxxx.....2.v....v.....v..v.33.xxx
xxxxxxxxxxx.....22v.............vv.2..xx
xxxxxxxxxxx.......vv..v........vv.33..xx
xxxxxxxxxx.....x233vv....T...vvv4444..xx
xxxxxxxxxx......4444v.........v.333....x
xxxxxxxxxx......33v4...v.......4x4.....x
xxxxxxxxxx.......334.....v.....4333....x
xxxxxxxxxx..c......3.3..4.4..v.......c.x
xxxxxxxxx...cc.....1...1.1.1...133..cc.x
xxxxxxxxx...cc....3.....4.4.........cc.x
xxxxxxxxx...cc...3...v........2.....cc.x
xxxxxxxxx...cc.........1...1.......cc..x
xxxxxxxxxx..cc.....1.....1.....1..ccc.xx
xxxxxxxxxx...ccc......3333333.....cc..xx
xxxxxxxxxx....cccc....3333333.....cc..xx
xxxxxxxxxx.....cccccc............cc...xx
xxxxxxxxxx........ccccc..O....cccc....xx
xxxxxxxxxxx........cccccccccccccc....xxx
xxxxxxxxxxx..........ccccccccccc.....xxx
xxxxxxxxxxxxx.......................xxxx
xxxxxxxxxxxxxxxx..................xxxxxx
xxxxxxxxxxxxxxxxxxxxx.......xxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

################################################################################
# Snake Pit: Coiled (minmay)
#
NAME:    minmay_snake_end_coiled
PLACE:   Snake:$
TAGS:    no_rotate
ORIENT:  southwest
MONS:    naga, naga warrior, naga mage, greater naga
SUBST:   1 = 11., 2 = 23., 4 = 444123..
SUBST:   c : c:20 v:5
KITEM:   O = serpentine rune of Zot
MAP
xxxxxxxxxxxxxxxxxc.@..cxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxc....cxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxcc....ccxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxc......cxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxc..1...cxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxc...21.cxxxccccccccxxxxxx
xxxxxxxxxxxcccccc......ccccc......cccxxxx
xxxxxxxccccc...2........2.....1.....ccccc
xxxxcccc............4....................
xxccc.....4.....2.....2.......2.1.......@
xcc...............2.2........1...........
xc..........ccccc......cccccc......cccccc
cc..1....ccccxxxc......cxxxxccccccccxxxxx
c.......ccxxxxxxc....1.cxxxxxxxxxxxxxxxxx
c...4...cxxxxxxxc......cxxxxxxxxxxxxxxxxx
c.......ccxxxxxxc.1.2..cxxxxxxxxxxxxxxxxx
cc....1..ccccxxxc......cxxxxxxxxxxxxxxxxx
xc..........ccccc......ccccccxxxxxxxxxxxx
xcc......4..................cccccxxxxxxxx
xxccc...........................ccccxxxxx
xxxxcccc..1....4...........1.......cccxxx
xxxxxxxccccc.........4...............ccxx
xxxxxxxxxxxcccccc......ccccc.....1....cxx
xxxxxxxxxxxxxxxxc......cxxxcccc.......ccx
xxxxxxxxxxxxxxxxc..1...cxxxxxxcc...4...cx
xxxxxxxxxxxxxxxxc...21.cxxxxxxxc.......cx
xxxxxxxxxxxxxxxxc..1...cxxxxxxcc...1...cx
xxxxxxxxxxxxxxxxc......cxxxcccc.......ccx
xxxxxxxxxxxcccccc..4...ccccc..........cxx
xxxxxxxccccc.........................ccxx
xxxxcccc2.......1.......2.....4....cccxxx
xxccc........1....4.............ccccxxxxx
xcc.......4.................cccccxxxxxxxx
xc.....1....ccccc......ccccccxxxxxxxxxxxx
cc.......ccccxxxc....1.cxxxxxxxxxxxxxxxxx
c....1..ccxxxxxxc.1....cxxxxxxxxxxxxxxxxx
c.......cxxxxxxxc....2.cxxxxxxxxxxxxxxxxx
c...4...ccxxxxxxc..1...cxxxxxxxxxxxxxxxxx
cc.......ccccxxxc2...1.cxxxxxxxxxxxxxxxxx
xc......2...ccccc......ccccccxxxxxxxxxxxx
xcc..1............1.......4.cccccxxxxxxxx
xxccc..........1.....4......1...ccccxxxxx
xxxxcccc.4...2..4..........12.4....cccxxx
xxxxxxxccccc..1.....2..1....1....4..Ocxxx
xxxxxxxxxxxcccccccccccccccccccccccccccxxx
ENDMAP

################################################################################
# Salamanders (by Mu.)
# by popular request
#
NAME:   snake_pit_salamanders_mu
PLACE:  Snake:$
ORIENT: north
TAGS:   no_rotate
MONS:   naga
MONS:   naga mage / nothing
MONS:   naga warrior / nothing
MONS:   greater naga / nothing
MONS:   guardian serpent / nothing
MONS:   w:7 anaconda / black mamba / nothing
KITEM:  O = serpentine rune of Zot
KMONS:  O = guardian serpent
KMONS:  S = guardian serpent
KFEAT:  7 = lava
KMONS:  7 = salamander / w:5 lava snake / nothing
KMASK:  l7 = no_monster_gen
NSUBST: l = 8:7
NSUBST: . = 6:1 / .
MAP
              cccccc
            ccc2452ccc
           cc..2232..cc
          cc..llllll..cc
          c..llllllll..c  ccccc
   ccccc cc.ll7llll7ll.cccc666cc
  cc...ccc..llllllllll..cc66666cc
 cc.222.cc.llllllllllll.+666S666c
 c..444..c3llllllllllll.+666O666c
 c.42443.+5llllllllllll.+666S666c
 c.43323.c3llllllllllll.cc66666cc
 cc.....cc..llllllllll..ccc666cc
 @cc...+ccc.ll7llll7ll.ccccccccccc
c+cccccc..c..llllllll..c...+.....c
c..lllc.3.cc..llllll..cc...cl.33.c
c.3lllcc...cc..2232..cc...ccll...c
cc..lllcc...ccc2452ccc...cclll..cc
 cc..l7lcc....cccccc....ccl7l..cc
  cc..lllcc.....44.....cclll..cc
   cc..lllcc....54....cclll..cc
    cc..lllccc..44..ccclll..cc
     cc...lllcccccccclll...cc
      cc...llllllllllll...cc
       cc..lllll77lllll..cc
        c....llllllll....c
        c......llll......c
        c.2242..ll..2452.c
        c.3222..ll..2223.c
        c......llll......c
        cccc+cllllllc+cccc
         c....ll77ll....c
         cc.3.llllll.3.cc
          cc..4llll4..cc
           cc..4224..cc
            cc......cc
             cccccccc
ENDMAP
