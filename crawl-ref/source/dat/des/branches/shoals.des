################################################################################
# shoals.des: All vaults for the Shoals.
################################################################################

{{

function shoals_finale_setup(e, qualifier, decoy_count)
  qualifier = "_" .. qualifier
  if not decoy_count then
    decoy_count = crawl.random_range(6, 8)
  end
  e.hook('post_place', function ()
    dgn.place_maps{tag=("shoal_rune" .. qualifier), count=1, die_on_error=true}
    dgn.place_maps{tag="shoal_guardian", count=1, die_on_error=true}
    dgn.place_maps{tag=("shoal_hut" .. qualifier), count=decoy_count,
                   die_on_error=true}
  end)
end

-- When the kraken dies, the tides will control the water, so the items will
-- be accessible!
function shoals_atoll_kraken_death(data, triggerable, triggerer, marker, ev)
    crawl.mpr("The surface of the water froths suddenly, and then rushes away.",
              "warning")

    local points = dgn.find_marker_positions_by_prop("water_marker", 1)
    for _, point in ipairs(points) do
        dgn.fprop_changed(point.x, point.y, "no_tide")
    end
    -- Force tide effects to be applied immediately.
    dgn.apply_tide()
end

function shoals_atoll_place_items(data, triggerable, triggerer, marker, ev)
    local points = dgn.find_marker_positions_by_prop("tide_seed", 1)
    for _, point in ipairs(points) do
        for i = 1, 5 do
            dgn.create_item(point.x, point.y, "star_item")
        end
    end
end
}}

##############################################################################
# Shoals entry vaults
##############################################################################

NAME:   shoals_dummy_entry
TAGS:   shoals_entry
KFEAT:  O = enter_shoals
MAP
O
ENDMAP

NAME:   minmay_shoals_entry_lonely_mermaid
TAGS:   shoals_entry no_monster_gen
ORIENT: float
KMONS:  1 = mermaid
KFEAT:  1 = w
SUBST:  Y = W.
SUBST:  y = Ww
KFEAT:  O = enter_shoals
MAP
     xx...@...xx
   xxxWY.....YWxxx
 xxxyWWWY...YWWWyxxx
xxwyWWWWxxxxxWWWWywxx
xwwyOWyxx   xxyW1ywwx
xwwwyyxx     xxyywwwx
xxwwwxx       xxwwwxx
 xxxxx         xxxxx
ENDMAP

NAME:   minmay_shoals_entry_hut
TAGS:   shoals_entry
MONS:   snapping turtle
SUBST:  W = WWWw, w = wwwW
TILE:   x = wall_vines
KFEAT:  O = enter_shoals
MAP
  WWWWWWW
 WWwwwwwWW
WWwxxxxxwWW
Wwxx1..xxwW
Wwx.....xwW
Wwx..O..+.@
Wwx.....xwW
Wwxx...xxwW
WWwxxxxxwWW
 WWwwwwwWW
  WWWWWWW
ENDMAP

NAME:   minmay_shoals_entry_centaurs
TAGS:   shoals_entry
MONS:   centaur / nothing w:5, plant / nothing w:20
KFEAT:  O = enter_shoals
MAP
   .......
  .2222222.
 .222...222.
.222.222...2.
.22.2222222.2
.2.22.112222.
.2.221O1222.2
.2.22.12.22.2
.22.2222.222.
.222....2222.
 .222222222.
  .22.2..2.
   ..2.22.
ENDMAP

NAME:   minmay_shoals_entry_beasts
TAGS:   shoals_entry
MONS:   hippogriff / manticore / griffon / nothing w:60
KFEAT:  O = enter_shoals
MAP
111
1O1
111
ENDMAP

NAME:   minmay_shoals_entry_sharks
TAGS:   shoals_entry
KMONS:  w = shark / nothing w:50
KFEAT:  w = w
KFEAT:  O = enter_shoals
MAP
 WWWWW
WWwwwWW
WwwwWwW
WwwOwwW
WwwwwwW
WWwwwWW
 WWWWW
ENDMAP

##############################################################################
# Non-ending non-entry vaults
##############################################################################

###########################
# Shoal Ruins - nothing special, just some flavor (Enne)
# Appear rarely.
#
NAME:   overgrown_hall
DEPTH:  Shoals
WEIGHT: 2
MONS:   plant, fungus
MONS:   cyclops / stone giant w:2
SUBST:  x = x:20 .
SUBST:  G = G:40 .
SUBST:  . = .:90 1:19 x:1
SUBST:  1 = 1 2:1
MAP
xxxxxxxxxxx
x.........x
x.G.G.G.G.x
+....3....+
x.G.G.G.G.x
x.........x
xxxxxxxxxxx
ENDMAP

NAME:   overgrown_tower
DEPTH:  Shoals
WEIGHT: 2
MONS:   plant, fungus
SUBST:  + = x +
SUBST:  x = x:60 . '
SUBST:  ' = ' 1:5
SUBST:  . = . 2:1
SUBST:  ' = .
MAP
 '''''''''
'''xx+xx'''
''xx...xx''
'xx.....xx'
'x.......x'
'x...<...x'
'x.......x'
'xx.....xx'
''xx...xx''
'''xxxxx'''
 '''''''''
ENDMAP

NAME:   overgrown_statue
DEPTH:  Shoals
WEIGHT: 1
MONS:   plant
MONS:   ice statue / orange crystal statue / silver statue
SUBST:  . = 1:1 .:30
SUBST:  p = 1:2 .
SUBST:  P = 1:20 .
SUBST:  G = G 2
MAP
  ........
 ..........
....ppp.....
...ppppp....
..ppPPPpp...
..ppPGPpp...
..ppPPPpp...
...ppppp....
....ppp.....
 ..........
  ........
ENDMAP

NAME:   shoals_oasis
DEPTH:  Shoals
WEIGHT: 1
MONS:   plant
SUBST:  . = 1:1 .
SUBST:  W = W .
SUBST:  w = W w
SUBST:  _ = w
MAP
  ...
 ..W..
..WwW..
.Ww_wW.
..WwW..
 ..W..
  ...
ENDMAP

NAME:   shoals_atoll
DEPTH:  Shoals
KMONS:  1 = patrolling kraken
KFEAT:  1 = deep_water
KPROP:  1Wgw. = no_tide
FHEIGHT: gw1 = 40
MARKER: 1 = lua:MonPropsMarker:new {ancient_kraken="ancient_kraken"}
MARKER: 1 = lua:portal_desc { water_marker = 1, tide_seed = 1 }
MARKER: W = lua:portal_desc { water_marker = 1 }
MARKER: w = lua:portal_desc { water_marker = 1 }
MARKER: . = lua:portal_desc { water_marker = 1 }
SUBST:  g = WWw, X = ccc., x = c
{{
local kraken_death_marker = TriggerableFunction:new {
  func="shoals_atoll_kraken_death",
  repeated=false,
  props= {water_marker="1"} }

kraken_death_marker:add_triggerer(DgnTriggerer:new {
  type="monster_dies",
  target="ancient_kraken" })

lua_marker("*", kraken_death_marker)

local place_items_marker = TriggerableFunction:new {
  func="shoals_atoll_place_items",
  repeated=false,
  props= {water_marker="1"} }

place_items_marker:add_triggerer(DgnTriggerer:new { type="entered_level" })
lua_marker("1", place_items_marker)
}}
MAP
     XXXXXXXX
    XxxxxxxxxX
   Xx...WWWGxxX
  Xx.gggggW*GxX
 Xx.ggwwwggWWxX
Xx.ggwwwwwggWxX
Xx.ggww1wwgg.xX
Xx.ggwwwwwgg.xX
XW..ggwwwgg.xX
 WW..ggggg.xX
  WW......xX
   WWWWxxxX
      XXXX
ENDMAP

NAME:   yaktaur_hedge
DEPTH:  Shoals
MONS:   bush
MONS:   yaktaur
MAP
.......
.11111.
.1...1.
.1.1.1.
.1.121.
.1.111.
.......
ENDMAP

NAME:   african_american_sheep
DEPTH:  Shoals
MONS:   sheep, sheep name:black n_adj n_spe col:darkgrey tile:mons_black_sheep
MAP
  ....
 ..11..
.12.1..
......
ENDMAP

NAME:   hangedman_shoal_octodias
DEPTH:  Shoals
TAGS:   mini_float
KMONS:  1 = octopode
KFEAT:  1 = deep_water
MAP
 xWxwxWx
xwwwxwwwx
Wwxw1wxwW
xww1x1wwx
wx1xwx1xw
xww1x1wwx
Wwxw1wxwW
xwwwxwwwx
 xWxwxWx
ENDMAP

NAME:    hangedman_shoal_volcano
TAGS:    no_monster_gen no_item_gen patrolling
DEPTH:   Shoals
KMONS:   12 = merfolk ; trident ego:flaming | spear ego:flaming . \
             robe ego:fire_resistance | robe w:5 . \
             buckler ego:fire_resistance | buckler | nothing w:30
KMONS:   3 = mermaid ; trident ego:flaming | spear ego:flaming . \
             robe ego:fire_resistance | robe w:5 . \
             buckler ego:fire_resistance w:20 | buckler | nothing w:70
KMONS:   4 = siren ; trident ego:flaming | spear ego:flaming . \
             robe ego:fire_resistance | robe w:5 . \
             buckler ego:fire_resistance w:20 | buckler | nothing w:70
KMONS:   5 = lava fish
KMONS:   67 = fire bat
KMONS:   8 = fire crab
KITEM:   67 = % w:15 / *
KITEM:   8 = * w:15 / |
KPROP:   l = no_cloud_gen
KPROP:   .1234"x = no_tide
: if crawl.random2(you.depth()+4) <= you.depth() then
SUBST:   3 = 4
SHUFFLE: 12 / 4"
: else
SHUFFLE: 12 / 3"
: end
: if crawl.random2(you.depth()+4) <= you.depth() then
SUBST:   7 = 8
: end
SUBST:   6 = 66*, X = xx., x : x:9 .:1
FTILE:   '678 = floor_pebble_red
COLOUR:  '678 = red
MAP
   WWWWWWWW
 WWWW.xx.WWWW
 WW..x..x..WW
WW.x2x''x"x.WW
WW.1''''''3.WW
W.xx'8ll6'xx.W
Wx.''ll5l''.xW
Wx.''l5ll''.xW
W.xx'6ll7'xx.W
WW.3''''''2.WW
WW.x"x''x1x.WW
WWW..x..x..WWW
 WWWW.xx.WWWW
   WWWWWWWW
ENDMAP

NAME:    st_cyclops
DEPTH:   Shoals:1-4
MONS:    cyclops, stone giant / frost giant
KMONS:   P = plant
KMONS:   f = fungus
SHUFFLE: 23
SUBST:   p = p., p = Pf, y = xP., x = x:20 c:2 P:2
SUBST:   2 = 21, 3 = 1
MAP
   ppppp
  yyyyyyy
 pyxxyxyyy.
pyxypppxx.1.
pyxpp1.px...
pyxp2.1..1.3
pyypp1.px...
pyxxpppyx.1.
 ypxxyxxyy.
  yyyyyyy
   ppppp
ENDMAP

NAME:    st_shoals_ruin
DEPTH:   Shoals:1-4
TAGS:    no_pool_fixup
MONS:    cyclops, frost giant, stone giant
KMONS:   P = plant
KMONS:   f = fungus
KFEAT:   Pfp = shallow_water
KFEAT:   5 = deep_water
SUBST:   p = ppWw, p = Pf, y = xxxxcccPPPfw, f = PPwW, x = x:10 c:1
SHUFFLE: 23
SUBST:   2 = 2:15 1:10, 3 = 3:15 1:10
MAP
   ppp  @ ppp
  pppxx..xxppp
 ppxxx.3.wfxxpp
ppxxpxx..xwfxxpp
ppxpppxxxWWffxpp
pxx**ppyfWWWfxxp
pxp1..pyfW..ffxp
pxp.21pyf..1.wxp
pxp1..yyy1...pxp
pxxp.1pyp..1pxxp
ppxp.......ppxpp
ppxxp..p.fppxxpp
 ppxxpyyyyfxxpp
  ppxxxxxxxxpp
   pppppppppp
ENDMAP

NAME:    st_sunken_huts
DEPTH:   Shoals:1-4
KMONS:   1 = merfolk javelineer
KMONS:   2 = merfolk impaler
KMONS:   3 = merfolk aquamancer
KMONS:   4 = merfolk
KMONS:   5 = mermaid / siren
KFEAT:   12345 = deep_water
SHUFFLE: 1235
NSUBST:  V = 1:w / *:x
NSUBST:  U = 1:w / *:x
NSUBST:  Y = 1:w / *:x
NSUBST:  X = 1:w / *:x
NSUBST:  Z = 1:w / *:x
SUBST:   W = wWW
MAP
      WWWWWWWWWWWWWWW
   WWWwwwwwwwxxxxwwwwWWW
 WWwxxxxwwwwwxw2xwwwwwwwWW
Wwwwx1wVww4wwUwwUww4wxYxxwW
WwwwxwwVwwwwwxUUxwwwwYwwxwW
WwwwxVVxwwwwwwwwwwwwwYw2xwW
WwwwwwwwwxZZxwwxXXxwwxYxxwW
Wwwww4wwwZwwZw5XwwxwwwwwwwW
 WWwwwwwwxw1xwwXw3xwwwwwWW
   WWWwwwxxxxwwxxxxwwWWW
      WWWWWWWWWWWWWWW
ENDMAP

NAME:    st_sunken_temple
DEPTH:   Shoals:1-4
TAGS:    no_pool_fixup
KMONS:   1 = merfolk javelineer
KMONS:   2 = merfolk impaler
KMONS:   3 = merfolk aquamancer
KMONS:   4 = merfolk
KMONS:   5 = mermaid / siren
KMONS:   6 = cyclops
KMONS:   7 = stone giant
KMONS:   8 = frost giant
KMONS:   9 = titan
KFEAT:   123456789 = deep_water
KITEM:   | = |
KITEM:   * = *
KFEAT:   |* = shallow_water
SHUFFLE: 12345 / 86769
NSUBST:  Z = 1:w / *:c
SUBST:   G = GGGwwW, 7 = 667, 8 = 688, 9 = 7899, x = c
MAP
   xxxxxxxxxxx
  xxGxGxGxGxGxx
 xxwwwwwwwwwwwxx
 xwww1wwwww1wwwx
xxwwxxxxZxxxxwwxx
xGwwx|wwwww*xwwGx
xxwwxw2www4wxwwxx
xGwwZwww5wwwZwwGx
xxwwxw4www2wxwwxx
xGwwx*wwwww*xwwGx
xxwwxxxxZxxxxwwxx
 xwww3wwwww3wwwx
 xxwwwwwwwwwwwxx
  xxGxGx+xGxGxx
   xxxxx@xxxxx
ENDMAP

NAME: nicolae_shoals_undead_eyrie
DEPTH: Shoals
SUBST: y = ..xx1
NSUBST: . = 4:1 / *:.
MONS: harpy zombie w:30 / griffon zombie / hippogriff zombie / bat zombie w:5
MAP
 .....
...y...
..yxy..
.yxxxy.
..yxy..
...y...
 .....
ENDMAP

NAME: nicolae_shoals_mertower
DEPTH: Shoals
FHEIGHT: ;'b123 = 40
KPROP: ;'b123 = no_tide
KMONS: 1 = merfolk
KMONS: 2 = merfolk impaler / nothing w:4
KMONS: 3 = merfolk javelineer / nothing w:4
KFEAT: ' = W
KFEAT: ;23 = w
KFEAT: b = .
MAP
  wwwwwwww
 wwwxxxxxww
 wwxx;22xxw
.b1x';;;3xw
.bb+';;;3xw
.b1x';;;3xw
 wwxx;22xxw
 wwwxxxxxww
  wwwwwwww
ENDMAP

NAME: nicolae_shoals_aquamancer_cove
DEPTH: Shoals
TAGS: no_monster_gen
KMONS: 123 = merfolk aquamancer / nothing w:3
SUBST: c = x.
KFEAT: 1 = w
KFEAT: 2 = .
KFEAT: 3 = W
MAP
     xxcc.@
    xx...2.
  xxxc.ccx.
xxxcc...cxxx
xccc.....ccxx
xc...3W3...cxx
x..WWwwwWW..cx
x.Ww1www1wW.cx
cw1wwwwwww1wcx
  wwwwwwwww
    wwwww
ENDMAP

NAME: nicolae_shoals_kraken_intersection
DEPTH: Shoals
TAGS: transparent no_pool_fixup water_ok no_trap_gen
SUBST: b = .w, z = x.
KPROP: w1. = no_tide
KPROP: c = no_rtele_into
SUBST: c = xw
KFEAT: 1 = w
KMONS: 1 = kraken
MAP
 ccxx@@xxcc
ccxxz..zxxcc
cxxz.bb.zxxc
xxz.bwwb.zxx
xz.bwwwwb.zx
@.bwww1wwb.@
@.bwwwwwwb.@
xz.bwwwwb.zx
xxz.bwwb.zxx
cxxz.bb.zxxc
ccxxz..zxxcc
 ccxx@@xxcc
ENDMAP

NAME: nicolae_shoals_arrival_archipelago
DEPTH: Shoals
SHUFFLE: {[(<, abc, def
SUBST: ade:@, bcf:W
MAP
     wwwwb
    www...b
 wwwww..{..b
wwwwwd.....w
ww..dd....ww
c....dwwffww
c..(.wwwffww
c....ee....w
ww..eee.[..w
wwwwwee....a
  wwww....a
    wwwwwa
ENDMAP

NAME:    hangedman_shoals_cup_caves
TAGS:    water_ok
DEPTH:   Shoals
MONS:    cyclops / stone giant w:2
MONS:    mermaid, harpy, snapping turtle, centaur / centaur warrior, sea snake
SHUFFLE: 234560, ABC
SUBST:   A : x:6 .:1, B : xxw., C : xw.., c = c:29 .:1, x = x:9 c:1, % = %:8 *:1 .:1
MAP
 xxxx        xxxxx  xxxx
xxxcxxxxW@Wxxxxcxxxxxcxxx
xW..%xxWABAWxx414xxx5..Wx
WA...2x.....x%...%x%...AW
@B.C.1c..C..c..C..c1.C.B@
WA...2x3...3x.....x%...AW
xW..%xxx%1%xxWABAWxx5..Wx
xxxcxxxxxcxxxxW@Wxxxxcxxx
 xxxx  xxxxx        xxxx
ENDMAP

##############################################################################
# Branch ends.
# Note that the Shoals usually have a randomised end map; predrawn maps
# (which need not be encompassing) are sometimes used, though.
##############################################################################

NAME: shoals_end_simple
PLACE: Shoals:$
: shoals_finale_setup(_G, 'simple')

# HangedMan's alternative Shoals ending
NAME: shoals_end_hangedman
PLACE: Shoals:$
WEIGHT: 8
: shoals_finale_setup(_G, 'hangedman', crawl.random_range(5,7))

################################################################################
# Shoal hut with rune inside.
#
# This hut is also used for the decoy huts, with the rune replaced with a good
# item.
#
NAME:      shoalhut_rune
TAGS:      shoal_rune_simple water_ok no_dump mini_float
SHUFFLE:   ABCD
SUBST:     A:x, B:x, C:x, D=+
SUBST:     0 = 0.
SUBST:     e = e.
KMONS:     ge = merfolk impaler / merfolk javelineer / merfolk aquamancer
LROCKTILE:  wall_vines
KITEM:     O = barnacled rune of Zot
MAP
 xxCxx
xx.0.xx
xe0g0ex
B0gOg0D
xe0g0ex
xx.0.xx
 xxAxx
ENDMAP

################################################################################
# Shoal decoy hut
#
NAME:      shoalhut
TAGS:      shoal_hut_simple water_ok no_dump allow_dup mini_float
SHUFFLE:   ABCD
SUBST:     A:x, B:x, C:x, D=+
SUBST:     0 = 0.
LROCKTILE:  wall_vines
MAP
 xxCxx
xx.0.xx
x.0.0.x
B0.|.0D
x.0.0.x
xx.0.xx
 xxAxx
ENDMAP

################################################################################
# Shoal hangout of Ilsuiw.
# The hut itself may not contain Ilsuiw if she had other engagements.

NAME:    shoal_ilsuiw
TAGS:    shoal_guardian
KMONS:   1 = Ilsuiw band, siren
KMONS:   2 = merfolk impaler / merfolk javelineer
SUBST:   3 = 33w
KMONS:   3 = merfolk aquamancer / merfolk impaler / merfolk javelineer
KFEAT:   123 = w
LROCKTILE:  wall_vines
MAP
 wxwxw
wxw2wxw
xwwwwwx
w3w1w3w
xwwwwwx
wxw2wxw
 wxwxw
ENDMAP

################################################################################
# HangedMan's alternative Shoals:$

NAME:    shoals_rune_alternative
TAGS:    shoal_rune_hangedman water_ok allow_dup transparent mini_float no_dump
KMONS:   12 = merfolk impaler / merfolk javelineer / merfolk aquamancer
KMONS:   3 = kraken / harpy band w:5
KMONS:   d = siren / mermaid w:5
KITEM:   d = barnacled rune of zot
KFEAT:   123 = w
KPROP:   d = no_tide
SHUFFLE: AB / AB / BA, CD / CD / DC
NSUBST:  2 = 2 = 22w / * = 22www, 0 = 2:0 / * = 00...
SUBST:   AC = x, B = w, D = .
LROCKTILE: wall_vines
FHEIGHT: .d0@ = 15
MAP
  xxxxxxx
 xx12wwwxx
xx2x..00Wxx
x1xd02w..Wx
x2.03Awx..x
xw.2AxBw..x
xw0wwBw.D.x
xw0.xw..C.x
xxW...DC.x@
 xxW....x..
  xxxxxx@.
ENDMAP

NAME:    shoals_rune_alternative_decoy
TAGS:    shoal_hut_hangedman water_ok allow_dup transparent mini_float no_dump
KMONS:   0 = 0
SHUFFLE: AB / AB / BA, CD / CD / DC
NSUBST:  0 = 3:0 / * = 00...
SUBST:   AC = x, B = w, D = .
FHEIGHT: .d0@ = 15
MAP
  xxxxxxx
 xxwwwwwxx
xxwx00.0Wxx
xwx|0ww..Wx
xw00wAwx..x
xw0wAxBw..x
xw.wwBw.D.x
xw0.xw..C.x
xxW...DC.x@
 xxW....x..
  xxxxxx@.
ENDMAP
