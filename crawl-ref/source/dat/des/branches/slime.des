###############################################################################
# slime.des: All maps and branch endings relating to the Slime Pits
###############################################################################

{{
function fixup_slime_vaults(data, triggerable, triggerer, marker, ev)
  if not dgn.persist.fix_slime_vaults then
    return
  end

  -- Restore teleport control.
  dgn.change_level_flags("!no_tele_control")

  local to_feat
  if you.god() == "Jiyva" and you.piety() > 160 then
    to_feat = "floor"
  else
    to_feat = "clear_rock_wall"
  end

  local seen = dgn.seen_replace_feat("stone_wall", to_feat)
        seen = dgn.seen_replace_feat("clear_stone_wall", to_feat) or seen

  if you.god() == "Jiyva" and you.piety() > 160 then
    if you.silenced() then
        crawl.mpr("An unexplained breeze blows through the dungeon.", "god")
    else
        crawl.mpr("You hear the sound of toppling stones.", "god")
    end
  else
    if seen then
        crawl.mpr("Suddenly, all colour oozes out of the stone walls.",
                  "monster_enchant")
    else
        crawl.mpr("You feel a strange vibration for a moment.",
                  "monster_enchant")
    end
  end

  local gxm, gym = dgn.max_bounds()
  for p in iter.rect_iterator(dgn.point(1, 1), dgn.point(gxm-1, gym-1)) do
    if dgn.fprop_at(p.x, p.y, "no_jiyva") then
      dgn.fprop_changed(p.x, p.y, "no_jiyva")
    end
  end

  -- We're done.
  dgn.persist.slime_fixup_done = true

  triggerable:remove(marker)
end
}}

##############################################################################
# Slime Pit entry vaults
##############################################################################

# Weight balancer
NAME:  slime_dummy
TAGS:  slime_entry
KFEAT: O = enter_slime_pits
MAP
O
ENDMAP

# Slime entries featuring an altar to Jiyva
NAME:   slime_altar_1
TAGS:   slime_entry
WEIGHT: 30
KFEAT:  Z = altar_jiyva
KFEAT:  O = enter_slime_pits
MAP
....
.OZ.
....
ENDMAP

NAME:   slime_altar_2
TAGS:   slime_entry
KFEAT:  Z = altar_jiyva
SUBST:  1 : 111223
NSUBST: 1 = 1 / 1. , 2 = 2 / 2... , 3 = 3 / 3....
MONS:   jelly / ooze / brown ooze / giant amoeba
MONS:   acid blob / slime creature w:5
MONS:   azure jelly
KFEAT:  O = enter_slime_pits
MAP
ccccc
c111m.
cO1_=@
c111m.
ccccc
ENDMAP

NAME:  dpeg_slime_entry_enclosed_altar
TAGS:  slime_entry
KFEAT: O = enter_slime_pits
KFEAT: S = slimy_wall
KFEAT: _ = altar_jiyva
MAP
  .....
 .ccccc.
.ccSSScc.
.cS...Sc.
.cS.O.cc.
.cS.._.+@
.ccScccc.
.ccccccc.
 .ccccc.
  .....
ENDMAP

NAME:   minmay_slime_entry_eyes
TAGS:   slime_entry ruin_lair
WEIGHT: 2
MONS:   eye of devastation, eye of draining / giant eyeball
KFEAT:  O = enter_slime_pits
KFEAT:  S = slimy_wall
MAP
 ....   ....
..xx..x..xx..
.xxxx.x.xxxx.
.xxx..x..xxx.
..x.S...S.x..
 ...........
 xxS.1O2.Sxx
 ...........
..x.S...S.x..
.xxx..x..xxx.
.xxxx.x.xxxx.
..xx..x..xx..
 ....   ....
ENDMAP

NAME:   minmay_slime_entry_pillars
TAGS:   slime_entry ruin_lair
MONS:   giant amoeba / brown ooze
KFEAT:  O = enter_slime_pits
MAP
...........
.ccc...ccc.
.ccc.1.ccc.
.ccc...ccc.
...........
..1..O..1..
...........
.ccc...ccc.
.ccc.1.ccc.
.ccc...ccc.
...........
ENDMAP

# In-game the boxes rarely stay closed due to ruination; this is intentional.
NAME:   minmay_slime_entry_jelly_boxes
TAGS:   slime_entry ruin_lair
WEIGHT: 5
MONS:   jelly / ooze w:1 / nothing w:1
KFEAT:  O = enter_slime_pits
MAP
.............
.xxxxx.xxxxx.
.xxx1x.x11xx.
.xx11x.xx1xx.
.xx1x...x1xx.
.xxxx....xxx.
......O......
.xxxx...xxxx.
.xx1.x.x.1xx.
.x11x.xx11xx.
.xx1x.xxxxxx.
.xxxxx.xxxxx.
.............
ENDMAP

NAME:   minmay_slime_entry_oklob_open
TAGS:   slime_entry
WEIGHT: 5
MONS:   oklob plant / nothing w:20
KFEAT:  O = enter_slime_pits
MAP
 .....
.......
..111..
..1O1..
..111..
.......
 .....
ENDMAP

NAME:   minmay_slime_entry_oklob_enclosed
TAGS:   slime_entry ruin_lair
ORIENT: northeast
WEIGHT: 5
MONS:   oklob plant / nothing w:20
KFEAT:  O = enter_slime_pits
KFEAT:  S = slimy_wall
MAP
xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxx......xxxxxx
xxSSxx...........xxxx
xxS111......xx....xxx
xSS.11....xxxx....xxx
xS..O11.xxxxx....xxxx
xSS..1xxxxxx.....xxxx
xxSSSSxxxxx.....xxxxx
xxxxxxxxxx.....xxxxxx
xxxxxxxx.....xxxxxxxx
xxxxxxx.@..xxxxxxxxxx
ENDMAP

NAME:   dpeg_slime_entry_corner
TAGS:   slime_entry ruin_lair
ORIENT: northwest
WEIGHT: 5
KFEAT:  O = enter_slime_pits
KFEAT:  S = slimy_wall
MAP
xxxxxxxxxxx
xSSSxxxxxx
xS...xxxx
xS.O...xx
xxxxxx.xx
xxxx....x
xxx...xx.
xx..
ENDMAP

################################################################################
# Slime Pit specific non-ending non-entry vaults
################################################################################
NAME:    hangedman_slime_spill
TAGS:    no_monster_gen no_trap_gen
DEPTH:   Slime:1-5
WEIGHT:  1
MONS:    patrolling eye of devastation
SHUFFLE: yzZ
SUBST:   2 = 00xxY, y = ., zZX = x., Y = x', { = {}, 0 : 00'''
KPROP:   02' = no_rtele_into
MAP
   ' ' '''Y.xx.@@@
  YY'Y'xxx'x.....@
 Yxxxxxx2xxX......
xxx222x0xxXX...XyX
 x20000xx......yXy
 x2202x.x.{.....1X
xx20000xx......yXy
 xx222x0xxXX...XyX
 Yxxxxxx2xxX......
  YY'Y'xxx'x.....@
   ' ' '''x.xx.@@@
ENDMAP

NAME:    hangedman_slime_tunnels
TAGS:    no_monster_gen no_trap_gen transparent
DEPTH:   Slime:1-5
WEIGHT:  1
KMONS:   1 = slime creature / brown ooze / giant amoeba / nothing w:2
KMONS:   2 = shining eye / giant eyeball / eye of devastation w:5 / \
             great orb of eyes / giant orange brain / random w:1
KFEAT:   2 = alarm trap / . w:30
SHUFFLE: AB, CD, EF, GH, ab
SUBST:   ACEG = ', BDFH = x, 0 = 0 ':4, ab = 0XX
SUBST:   X = x', Y = x., } = {} .:30, 0 : 00'''
KMASK:   ' = !no_monster_gen
KMASK:   '0x = opaque
KPROP:   '0 = no_rtele_into
MAP
  X'            'X
 X'X'X x  x  x XX'X
X'xxxxxxxxxxxxxxxx'X
'xx0x0x00xx00x0x0xx'
'x'x0x'xx0axx'x0x'x'
'xAxxxxBxaXxCxxxxDx'
'xxAxxBxxxxxxCxxDxx'
.Y.Y..Yx2xY.YY..YY..
..Y....xx1.........@
@................}.@
@..................@
@.........1xx....Y.@
..YY..YY.Yx2xY.xY...
.xxExxFxxxxxxGxxHxx'
'xExxxxFxbXxGxxxxHx'
'x'x0x'xx0bxx'x0x'x'
'xx0x0x00xx00x0x0xx'
X'xxxxxxxxxxxxxxxx'X
 X'XX x  x  x X'X'X
  X'            'X
ENDMAP

NAME: nicolae_amoeba_pool
DEPTH: Slime
TAGS: transparent mini_float
MONS: giant amoeba
ORIENT: float
SHUFFLE: abcd
SUBST: ab : @, cd : .
KPROP: f = no_rtele_into
SUBST: f = w.1
KMASK: w = no_monster_gen
KMASK: W = no_monster_gen
MAP
   cccc
 ..........
d.w.wwwf..w.
d..wwfwwf....
d.fwwfffwffbb
d..fwwwfww.b
 ...ffwwww.b
  ..wffw...b
  ..wfwfw.fw
 ...ffffw.ww
.ff.wwwwf.f
 .....f..ff
    aaaa..fw
ENDMAP

################################################################################
# Slime Pit
################################################################################
NAME:    slime_pit
PLACE:   Slime:$
ORIENT:  encompass
TAGS:    no_rotate no_dump
MONS:    the royal jelly
MONS:    acid blob
MONS:    great orb of eyes / nothing
SUBST:   ' : ' .:1,  ' : ' x:1,  ' = .x
SUBST:   " : " .:3,  " = .c
NSUBST:  P = O / *|
KMASK:   |O* = no_monster_gen
KPROP:   |O* = no_jiyva
KPROP:   |O* = no_rtele_into
KFEAT:   Z = altar_jiyva
KITEM:   O = slimy rune of Zot
KITEM:   * = star_item
SHUFFLE: ([{
LFLAGS:  no_tele_control

{{
-- Attach function to the royal jelly itself. This will follow it wherever
-- it goes, even through polymorphs, and set dgn.persist.royal_jelly_dead
-- when it dies through means other than being banished.
local function monster_die(monster, killer_type, killer_index, silent, wizard)
    if killer_type == "banished" then
      crawl.mpr("You feel a great sense of loss, and a brush of the abyss.")
    else
      dgn.persist.fix_slime_vaults = true
    end
end
}}
MARKER: 1 = lua: MonPropsMarker:new {monster_dies_lua_key = monster_die}

{{
local fixup_marker = TriggerableFunction:new(
  {
    func="fixup_slime_vaults",
    repeated=true
  }
)

fixup_marker:add_triggerer(DgnTriggerer:new {
  type="monster_dies",
  target="any"
})

fixup_marker:add_triggerer(DgnTriggerer:new {
  type="entered_level"
})

}}
# Doesn't matter where thisw marker goes, so we might as well chose
# the altar.
: lua_marker("Z", fixup_marker)

{{
    set_feature_name("stone_wall", "rune carved stone wall")
}}
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx''''xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx''xxxx''''...''xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxx'''..'''''........'''xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxx'.....................'xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxx'.......................''x'xxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxx'..........................'.xxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxx'............................'xxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxx'.............................'''xxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxx'...............................''xxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxx''..(..............................''xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxx'''....................................''xxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxx'.........................................'xxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxx''.........................................'xxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxx'...........................................'xxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxx''xx'..........................................'xxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxx'..''................""""."""""...........]....''xxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxx'x.................."cccc2ccccc".................'xxxxxxxxxxxxxxxxx
xxxxxxxxxxxxx'.'................"cc*cc..cc*cc".................''xxxxxxxxxxxxxxx
xxxxxxxxxxxxx'................."cc***cc4c***cc".................xxxxxxxxxxxxxxxx
xxxxxxxxxxxxx'................"cc*|*cc..cc*|*cc"................'xxxxxxxxxxxxxxx
xxxxxxxxxxxx''..............."cc*|P|*c4cc*|P|*cc"...............'xxxxxxxxxxxxxxx
xxxxxxxxxxxx'................"cc**|*cc..cc*|**cc"................'''xxxxxxxxxxxx
xxxxxxxxxxxx'................"ccc**c|cc4c|c**ccc"..................xxxxxxxxxxxxx
xxxxxxxxxxx'................."cccccccc.3cccccccc"..................'xxxxxxxxxxxx
xxxxxxxxxxx'................."c.4.c.4.1..4.c.4.c"..................''xxxxxxxxxxx
xxxxxxxxxxx'..................2.c.4.c..Z.c.4.c.2....................'xxxxxxxxxxx
xxxxxxxxxx'..........)......."cccccccc3.cccccccc"...................'xxxxxxxxxxx
xxxxxxxxxx'.................."ccc**c|cc4c|c**ccc"...................'xxxxxxxxxxx
xxxxxxxxxx'.................."cc**|*cc..cc*|**cc"..................'xxxxxxxxxxxx
xxxxxxxxx'..................."cc*|P|*c4cc*|P|*cc"..................'xxxxxxxxxxxx
xxxxxxxxx''..................."cc*|*cc..cc*|*cc"..................'xxxxxxxxxxxxx
xxxxxxxxxxx'..................."cc***cc4c***cc"...................'xxxxxxxxxxxxx
xxxxxxxxxxxx'..................."cc*cc..cc*cc"....................'xxxxxxxxxxxxx
xxxxxxxxxxxx'...................."cccc2ccccc"....................'xxxxxxxxxxxxxx
xxxxxxxxxxxxx'...................."""".""""".....................'xxxxxxxxxxxxxx
xxxxxxxxxxxxx'...........................................[........'xxxxxxxxxxxxx
xxxxxxxxxxxx''..............................................'''....'xxxxxxxxxxxx
xxxxxxxxxxxx'.............................................''xxx''.'xxxxxxxxxxxxx
xxxxxxxxxxxxx'..........................................''xxxxxxx.xxxxxxxxxxxxxx
xxxxxxxxxxxxx'''.......................................'xxxxxxxxx.xxxxxxxxxxxxxx
xxxxxxxxxxxxxxxx'......................................'xxxxxxxxx'xxxxxxxxxxxxxx
xxxxxxxxxxxxxxxx'.......................................'xxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxx''.....................................'xxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxx'.....................................'xxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxx''....................................'xxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxx'...................................'xxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxx'............................}......'xxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxx''................................'xxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxx'.............................''xxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxx'.............................'xxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxx'......................''...'xxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxx'''''........{.......'xx'..xxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx'''............'xxxx''xxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx''....'....'xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx''''x...''xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx''''xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP
