###
#
## Maps from named Wizards, unrand and fixedarts
#
# Tukima's Studio
#    threat: 6 dancing weapons, dancing weapon machine generating up to 5 more
#    loot:   the weapons, tukima's book and a scroll of acquirement
#    xp:     14400 or so
#    layout: mirrors; grey and white
#
# Eringya's Formal Garden
#    crumbling stone walls.
#    threat: centaurs, oklobs
#    loot:   a staff and four spellbooks
#    xp:     10500 or so
#    layout: formal (French) garden, flowers, crumbling stone walls
#
# Doroklohe's Tomb
#    threat: either Tomb set or demons or golems
#    loot:   either scrolls or armours or random items
#    xp:     10000+
#    layout: lots of tombs with loot and monsters inside
#    colour: yellow tombs, rest is dark, floor is randomised
#
# Wucad Mu's Monastery
#    threat: "monks", OCS
#    loot:   books, staff of wucad mu
#    xp:     9000+
#    layout: temple-esque!
#    colour: dark walls, red and orange.
#
# Cigotuvi's Fleshworks
#    threat: lumps, uglies, abominations
#    loot:   tmut. potions, books, rMut, ?oSumm, \oDeath
#    xp:     16000+
#    layout: organic, irregular, twisty, NO SQUARES, NO HARD CORNERS,
#            NOTHING THAT LOOKS PRETTY
#    colour: lightred walls, red doors, magenta floor, yellow 'glass'
#
# Iskenderun's Mystic Tower
#    threat: naga mystics, statues with IOOD and IMB, purple draconians
#    loot:   conjurations-themed randart books
#    xp:     20000+
#    layout: weird fortress tower thing with strange corners
#    colour: purple.
#
# Zonguldrok's Mausoleum
#    threat: zombies, ancient lich, skele warriors
#    loot:   scrolls, potions, spell books and zonguldrok's sword
#    xp:     20000+
#    layout: graveyard
#    colour: darkgrey, lightgrey
#
# The Roulette of Golubria
#    threat: distortion, tloc-themed monsters, Abyss monsters
#    loot:   tloc-themed consumables / rings / books, abyss escape aides, cloaks
#    xp:     20000+
#    layout: large maw shape with roulette through teeth, old-abyss-esque innards
#    colour: green floor, green/rarely cyan walls
#
## Random maps
#
# Cloud Mage's Chambers
#    threat: cloud mage (random name), vapours, air elementals
#    loot:   potions, robe of clouds, air-related spells
#    xp:     20000+
#    layout: "cloud"-esque, clouds.
#    colour: white, silver, lightblue
#
# Hellbinder's Lair:
#    threat: demons, Hellbinder
#    loot:   demonology-related items
#    xp:     15000+
#    layout: sigils, triangles and circles
#    colour: red, darkgrey
#
##
#
# TO-DO:
#
# The flavour messages for the portal vault require flavour, while the actual
# portals themselves could do with an overhaul and some new ones.
#
# *MUST* make these look awesome in tiles, too.
#
##

{{
function wizlab_portal (e)
    local timeout_turns_long = crawl.random_range(2700, 3300)
    local timeout_turns_short = timeout_turns_long/10
    local messager =
        timed_msg {
            visible = true,
            -- $F{xxx} will be substituted with the 'entity' property of the timed
            -- marker, or with the desc property (if entity is not set).
            messages = time_messages(timeout_turns_short,
                            "$F{The}이(가) 약하게 빛나기 시작했다.",
                            "$F{The}이(가) 살며시 진동하기 시작했다.", 
                            "$F{The}은(는) 살짝 투명해졌다.",
                            "$F{The}이(가) 거의 사라져 형체를 잃어가고 있다.")
    }

    e.lua_marker('O',
      timed_marker {
        disappear = "관문이 완전히 사라졌다.", 
        desc = "마법의 관문",
        entity = 'portal',
        dst = 'WizLab',
        overview = "마법의 관문",
        turns = timeout_turns_long,
        turns_short = timeout_turns_short,
        floor = "stone_arch",
        feat_tile = "dngn_portal_bazaar_gone",
        msg = messager })
    e.kfeat("O = enter_portal_vault")
    e.tile("O = no_random dngn_portal_wizard_lab")
    e.tags("uniq_wizlab no_item_gen no_monster_gen chance_wizlab")
    e.chance("5%")
end

-- Destinations:
function wizlab_setup (e, wizlab_desc)
    e.kfeat("< = exit_portal_vault")
    e.tile("< = no_random dngn_portal_wizard_lab")
    -- This is so that the note shows up properly.
    e.set_feature_name("exit_portal_vault", "밖으로 나가는 관문")
    if wizlab_desc ~= "Eringya's Formal Garden" then
      e.set_feature_name("stone_arch", "텅 빈 아치형 돌무더기")
    end
end

function wizlab_milestone (e, wizlab_desc)
    crawl.mark_milestone("br.enter", wizlab_desc .. "에 입장.", "parent")
end

function tukima_machine (data, triggerable, triggerer, marker, ev)
  if triggerer.type ~= "turn" or triggerer.sub_type ~= "countdown" then
    return
  end

  local x, y = marker:pos()
  local you_x, you_y = you.pos()

  if data.weapon_count >= 5 then
    if you.see_cell(x, y) then
      crawl.mpr("기계에서 둔탁한 소음이 들렸다.")
    else
      crawl.mpr("멀리서 무언가 둔탁한 소음이 들려왔다.")
    end
    return
  end

  if (you_x == x and you_y == y) then return end
  if dgn.mons_at(x, y) then return end

  dgn.apply_area_cloud(x, y, 1, 6, 1, 10, "black smoke", "other", -1)

  if (not dgn.create_monster(x, y, "generate_awake dancing weapon")) then
    return
  end

  data.weapon_count = data.weapon_count + 1

  if you.see_cell(x, y) then
    crawl.mpr("기계에서 찢어지는 듯한 소리와 함께, 날아다니는 무기들이 튀어나왔다!")
  else
    crawl.mpr("멀리서 찢어지는 듯한 소음이 들렸다.")
  end
end

function wizlab_eringya_grow(data, triggerable, triggerer, marker, ev)
  if triggerer.type ~= "turn" or triggerer.sub_type ~= "countdown" then
    return
  end

  local count = 0

  if crawl.one_chance_in(5) then
    -- Fruit!
    for spot in iter.adjacent_iterator() do
      if not feat.is_solid(spot:xy()) and feat.has_solid_floor(spot:xy())
         and crawl.one_chance_in(3) then
        dgn.create_item(spot.x, spot.y, "pear / apple / choko / apricot / orange"
                          .. "/ banana / strawberry q:4 / lemon / rambutan "
                          .. "/ grape q:7 / sultana q:9 / lychee")
        count = count + 1
      end
    end
    if count > 0 then
      crawl.mpr("주위에 과일들이 자나랐다!")
    end
  else
    for spot in iter.adjacent_iterator() do
      if not feat.is_solid(spot:xy()) and feat.has_solid_floor(spot:xy())
         and not crawl.one_chance_in(3) then
        dgn.create_monster(spot.x, spot.y, "toadstool")
        count = count + 1
      end
    end
    if count > 0 then
      crawl.mpr("주위에 버섯들이 자라났다!")
    end
  end
end

function wizlab_doroklohe_convert_boxes(data, triggerable, triggerer,
                                        marker, ev)
  if triggerer.type ~= "turn" or triggerer.sub_type ~= "countdown" then
    return
  end

  if data.phase == 1 then
    data.phase = data.phase + 1
    if you.silenced() then
      crawl.mpr("땅이 흔들렸다.", "warning")
    else
      crawl.mpr("희미하면서도 째지는 듯한 소음이 들렸다.", "warning")
    end
    return
  elseif data.phase == 2 then
    data.phase = data.phase + 1
    if you.silenced() then
      crawl.mpr("땅이 흔들렸다.", "warning")
    else
      crawl.mpr("무엇인가 갈려나가는 요란스러운 소음이 들렸다.", "warning")
    end
    return
  elseif data.phase == 3 then
    data.phase = data.phase + 1
    crawl.mpr("갑자기 벽이 무너져내렸고, 갇혀 있던 존재들이 뛰쳐나왔다!", "warning")
    for _, i in ipairs(dgn.find_marker_positions_by_prop("wall_drop", 1)) do
      dgn.terrain_changed(i.x, i.y, "floor", false, false, false)
      dgn.place_cloud(i.x, i.y, "black smoke", 2)
    end
    return
  else
    return
  end
end

local function spawn_zombie (x, y, spec)
    dgn.terrain_changed(x, y, "floor", false, false, false)
    local mons = dgn.create_monster(x, y, spec)
    mons.set_prop("zonguldrok", 1)
    return mons
end

function wizlab_zonguldrok_spawn_zombies(data, triggerable, triggerer,
                                         marker, ev)
    if triggerer.type ~= "turn" or triggerer.sub_type ~= "countdown" then
        return
    end

    local graves = dgn.find_markers_by_prop("grave", 1)
    local x, y
    for _, gmarker in ipairs(graves) do
        x, y = gmarker:pos()
        if (you.see_cell(x, y) and crawl.one_chance_in(3)) or
            crawl.one_chance_in(8) then
            spawn_zombie(x, y, "generate_awake human zombie")
            gmarker:remove()
        end
    end
end

function wizlab_zonguldrok_killed_zombie(data, triggerable, triggerer,
                                         marker, ev)
    if data.door_appeared then
        return
    end

    local mons = dgn.mons_from_mid(ev:arg1())
    if not mons.has_prop("zonguldrok") then
        return
    end

    data.number_killed = data.number_killed + 1
    if #dgn.find_markers_by_prop("grave", 1) < data.number_killed then
        local x, y = marker:pos()
        dgn.tile_feat_changed(x, y, nil)
        dgn.terrain_changed(x, y, "closed_door", false, false, false)
        data.door_appeared = true
        if you.see_cell(x, y) then
            crawl.mpr("문이 나타났다!")
        else
            crawl.mpr("벽이 진동했다.")
        end
    end
end

function wizlab_zonguldrok_awaken_wizard(data, triggerable,
                                         triggerer, marker, ev)
    if data.awoke == true then
        return
    end

    local pos = dgn.find_marker_positions_by_prop("wizard_grave", 1)[1]
    local lich = dgn.create_monster(pos.x, pos.y, "generate_awake ancient lich "
                 .."col:brown name:존굴드로크의_리치 name_replace name_descriptor"
                 .." tile:mons_zonguldrok_lich")
    if lich then
        data.awoke = true
    end
end

function wizlab_wucad_msgfn(data, triggerable, triggerer, marker, ev)
  if data.trig == true then
    return
  end

  if crawl.one_chance_in(3) then
    return
  end

  if dgn.persist.wucad_mu_died == true then
    return
  end

  if data.spot == 1 then
    crawl.mpr("기이한 그림자 형체들이 눈 앞에서 춤추듯 나타났다가 곧 사라졌다.", "talk_visual")
    data.trig = true
  elseif data.spot == 2 then
    crawl.mpr("이 방은 그림자의 형체들로 사라졌다. 꼭 명상을 하고 있는 모습이다.", "talk_visual")
    data.trig = true
  elseif data.spot == 3 then
    crawl.mpr("유령 수도승들이 꼭 무술과 같은 복잡한 자세를 취하다가, 순식간에 사라져 없어졌다.", "talk_visual")
    data.trig = true
  elseif data.spot == 4 then
    if you.silenced() then
      return
    end
    crawl.mpr("어디선가 희미한 웃음소리가 들려왔다. 너무 희미해, 진짜였는지 환청이었는지도 헷갈린다.", "talk_visual")
    data.trig = true
  elseif data.spot == 5 then
    if you.silenced() then
      return
    end
    crawl.mpr("희미한 고통의 비명이 쭈그리고 앉은 형상으로부터 새어나왔다. 이 소리 역시, 빠르게 사라졌다.", "talk_visual")
    data.trig = true
  elseif data.spot == 6 then
    crawl.mpr("회색빛의 수도승들이 분수 주변을 돌아다닌다. 그들은 아무 말도 하지 않고, 서로를 쳐다보지도 않는다.", "talk_visual")
    data.trig = true
  elseif data.spot == 7 then
    crawl.mpr("석상 하나가 자리에 앉고 조용히 명상을 시작한다. 이것은 당신을 가리키고는, 격렬한 손동작을 취하고, 곧 사라졌다.", "talk_visual")
    data.trig = true
  end
end

function wizlab_wucad_summon_monks(data, triggerable, triggerer, marker, ev)
  if triggerer.type ~= "turn" or triggerer.sub_type ~= "countdown" then
    return
  end

  if dgn.persist.wucad_mu_died == true then
    return
  end

  -- For now, no spawning during Step From Time; perhaps monks should
  -- continue to appear, just more slowly. If so, the circle_iterator below
  -- needs a different centre from (0,0).
  if you.pos() == 0 then
    return
  end

  local msp = "human name:수도승 n_suf col:white hd:10 dur:2 sum:shadow_creatures" ..
            " nas:old_memories tile:mons_human_monk seen / deep troll name:수도승 n_suf"..
            " dur:2 seen sum:shadow_creatures nas:old_memories tile:mons_deep_troll_monk" ..
            " / iron troll name:수도승 n_suf col:white dur:2 sum:shadow_creatures " ..
            " nas:old_memories seen tile:mons_iron_troll_monk w:5"

  local count = 0
  for point in iter.circle_iterator(3) do
    if crawl.one_chance_in(8) and count < 3 then
      if feat.is_solid(point.x, point.y) or not feat.has_solid_floor(point.x, point.y) then
      else
       local mon = dgn.create_monster(point.x, point.y, msp)
       count = count + 1
      end
    end
  end

  crawl.redraw_view()

  if count == 1 then
    crawl.mpr("그림자 하나가, 자신의 형체를 찾아가기 시작했다!", "warning")
  elseif count > 1 then
    crawl.mpr("그림자는 자신의 형체를 찾았다.", "warning")
  end
end

function callback.wizlab_wucad_wucad_died(data, triggerable,
                                           triggerer, marker, ev)
  dgn.persist.wucad_mu_died = true
  crawl.mpr("그림자들은 이 곳에서 사라져, 영원히 떠났다.", "talk_visual")
end

function teleporter_golubria_switch_fn(data, triggerable, triggerer, marker, ev)
  local position = dgn.point(marker:pos())
  my_slaves = dgn.find_marker_positions_by_prop("teleport_spot",
                                                data.teleport_spot)

  if you.teleport_to(my_slaves[1].x, my_slaves[1].y, true) then
    crawl.mpr("당신 주변의 모습이 갑자기 변화했다!")
  else
    crawl.mpr("어디선가 날카로운 쇳소리가 들렸다.")
  end
end

}}

default-depth: D:22-27, Elf, Crypt, Vaults:1-4

###############################################################################
# Portal entrances.
#
# Todo: more, and better.
NAME:       mu_enter_wizlab_1
TILE:       m = dngn_transparent_wall_green
COLOUR:     m = lightgreen
:           wizlab_portal(_G)
MAP
    wwwww
   wwwwwww
  wwbmbmbww
 wwbb...bbww
wwbm..O..mbww
 wwbb...bbww
  wwbb+bbww
   www.www
    w.@.w
ENDMAP

NAME:       mu_enter_wizlab_2
DEPTH:      D:22-27, Elf, Crypt, Vaults:1-4
SHUFFLE:    tU
TILE:       m = dngn_transparent_wall_green
COLOUR:     m = lightgreen
TILE:       c = wall_stone_gray
:           wizlab_portal(_G)
MAP
.................
.bbbbbbbmbbbbbbb.
.bb...b...b...bb.
.m..U...O...U..m.
.bb...b...b...bb.
.bbbbbbb+bbbbbbb.
..bb...b.b...bb..
..m..t.....t..m..
..bb...b.b...bb..
..bbbbbb+bbbbbb..
.................
......c...c......
.....ccc.ccc.....
.................
ENDMAP

NAME:       mu_enter_wizlab_3
MARKER:     ! = lua:fog_machine { \
                 pow_max = 10, delay_min = 10, delay_max = 40, \
                 size = 2, size_buildup_amnt = 5, \
                 size_buildup_time = 25, cloud_type = "flame" \
             }
MARKER:     ? = lua:fog_machine { \
                 pow_max = 10, delay_min = 10, delay_max = 40, \
                 size = 2, size_buildup_amnt = 5, \
                 size_buildup_time = 25, cloud_type = "freezing vapour" \
             }
COLOUR:     .' = red / blue
KPROP:      '?! = no_rtele_into
SUBST:      ' = .
TILE:       n = dngn_transparent_stone_magenta
COLOUR:     n = lightmagenta
TILE:       c = wall_stone_gray
:           wizlab_portal(_G)
MAP
 ccccccccc
cc''!'?''cc
c?'''''''!c
c''nnnnn''c
c''n...n''c
c''n.O.n''c
c'?n...n!'c
c''nn.nn''c
c''nn.nn''c
cc!n.n.n?cc
 ccnn.nncc
  cc.n.cc
   cc.cc
    c@c
ENDMAP

NAME:       mu_enter_wizlab_4
KFEAT:      ! = floor
COLOUR:     ! = magenta
FTILE:      !O = floor_rough_magenta
:           wizlab_portal(_G)
MAP
    xxxxx
    w!!!x
   ww!O!x
  www!!!x
 www..www
www..www
ww..www
w..www
@.www
@www
ENDMAP

NAME:       mu_enter_wizlab_5
COLOUR:     U = random
COLOUR:     W = mutagenic
KFEAT:      o = granite_statue
FTILE:      WU.Oo = floor_rough_magenta
TILE:       b = dngn_crystal_magenta
:           wizlab_portal(_G)
: set_feature_name("green_crystal_wall", "수정 벽")
MAP
bbbbbbbbbbb
bbbb...bbbb
bWWU.O.UWWb
bWbb...bbWb
bWWWW.WWWWb
bbbbW.Wbbbb
bbbbW.Wbbbb
bbbbW.Wbbbb
bbbbW.Wbbbb
bbbbo.obbbb
bbbb...bbbb
     @
ENDMAP

NAME:       mu_enter_wizlab_6
SUBST:      x = mc.
TILE:       c = wall_stone_white
TILE:       m = dngn_transparent_stone_magenta
FTILE:      mcx.O = floor_rough_magenta
:           wizlab_portal(_G)
MAP
c.c.ccccccc
x.x.x.x..Oc
.x.x.x.x..c
x.x.x.x.x.c
.x.x.x.x.xc
x.x.x.x.x.c
.x.x.x.x.xc
x.x.x.x.x..
.x.x.x.x.xc
x.x.x.x.x..
.x.x.x.x.xc
ENDMAP

NAME:   mu_enter_wizlab_7
KFEAT:  abcd : floor
KFEAT:  ABCD : rock_wall
TILE:   ABCD = floor_rough_white
TILE:   ABCD = wall_zot_white
COLOUR: Aa : yellow
COLOUR: Bb : green
COLOUR: Cc : blue
COLOUR: Dd : magenta
: wizlab_portal(_G)
MAP
xxxxxxxxxxxxx
x...........@
x.xxxxxxxxxxx
xaAbbbbbbbbax
xaAcBBBBBBAax
xaAcCddddcAax
xaAcCdDDCcAax
xaAcCdOdCcAax
xaAcCDDdCcAax
xaAcddddCcAax
xaABBBBBBcAax
xabbbbbbbbAax
xxxxxxxxxxx.x
@...........x
xxxxxxxxxxxxx
ENDMAP

NAME:     infiniplex_enter_wizlab_glasses
DEPTH:    D:22-27, Elf, Crypt, Vaults:1-4
FTILE:    GO_ = floor_rough_magenta
TILE:     m = dngn_transparent_wall_green
COLOUR:   m = lightgreen
COLOUR:   _ = magenta
TILE:     G = dngn_statue_dragon
: wizlab_portal(_G)
MAP
.......
.bmmmb.
.m_O_m.
.mG_Gm.
.m___m.
.bb_bb.
.......
ENDMAP

NAME:     infiniplex_enter_wizlab_water
DEPTH:    D:22-27, Elf, Crypt, Vaults:1-4
FTILE:    cnO_ = floor_rough_magenta
TILE:     n = dngn_transparent_stone_magenta
COLOUR:   n = lightmagenta
COLOUR:   _ = magenta
TILE:     c = wall_stone_white
: wizlab_portal(_G)
MAP
ww......ww
ww.wwww.ww
...wwww...
.wwcnncww.
.wwnO_nww.
.wwn__nww.
.wwcnn_ww.
...wwww...
ww.wwww.ww
ww......ww
ENDMAP

NAME:     infiniplex_enter_wizlab_spiral
DEPTH:    D:22-27, Elf, Crypt, Vaults:1-4
SUBST:    G : xG.
SUBST:    x : xx.
MARKER:   ! = lua:fog_machine { \
               pow_max = 8, delay_min = 20, delay_max = 60, \
               walk_dist = 3, size = 1, start_clouds = 1, \
               start_clouds = 1, cloud_type = "freezing vapour" \
           }
MARKER:   ? = lua:fog_machine { \
               pow_max = 8, delay_min = 20, delay_max = 60, \
               walk_dist = 3, size = 1, start_clouds = 1, \
               start_clouds = 1, cloud_type = "mutagenic fog" \
           }
FTILE:    _! = floor_frozen
COLOUR:   _! = lightcyan
FTILE:    -? = floor_rough_magenta
COLOUR:   -? = magenta
: wizlab_portal(_G)
MAP
xx...........xx
xG.bbbbbbbbb.Gx
...bw..........
.b.bw.bbbbbbbb.
.b.bw.bllllllb.
.b.bw.bl.....b.
.b.bw..l.bbb.b.
.b.bwwwO--?b.b.
.b.bbb._..-b.b.
.b....._b.?b.b.
.b!__!_!b.-b.b.
.bbbbbbbb.-b.b.
..........?b...
xG.bbbbbbbbb.Gx
xx...........xx
ENDMAP

###############################################################################

default-depth: WizLab

###############################################################################
# And arrivals!
###############################################################################
# Firstly, we have Tukima's Dance Studio.
#
NAME:       wizlab_tukima
ORIENT:     encompass
TAGS:       no_item_gen no_monster_gen no_rotate allow_dup
MONS:       dancing weapon
KFEAT:      M = granite_statue
TILE:       M = dngn_machine_tukima
MARKER:     M = lua:props_marker {veto_fragmentation="veto", veto_disintegrate="veto", \
                                  veto_shatter="veto", slaved_to="tukima"}
{{
local tukima_marker = TriggerableFunction:new {
    func = "tukima_machine",
    repeated = true,
    data = {weapon_count=0},
}

tukima_marker:add_triggerer(DgnTriggerer:new {
    type="turn",
    delay_min=500,
    delay_max=800
})

lua_marker('?', tukima_marker)

if crawl.coinflip() then
  subst("Y = +")
  subst("y = .")
  subst("Rr = c")
else
  subst("Y = c")
  subst("y = m")
  subst("R = .")
  subst("r = +")
end

if crawl.coinflip() then
  subst("X = +")
  subst("x = .")
  subst("Ll = c")
else
  subst("X = c")
  subst("x = m")
  subst("L = .")
  subst("l = +")
end
}}
SUBST:      ? = .
COLOUR:     +cA< = darkgrey
TILE:       c = wall_stone_dark
TILE:       V = dngn_dry_fountain_white
LFLOORTILE: floor_pebble_white
COLOUR:     mMV = silver
TILE:       m = dngn_mirror_wall
KMONS:      8 = ice statue col:silver spells:blink_other;blink_other;blink_other;\
                    blink_other;blink_other name:이상한_기계 \
                    name_replace name_descriptor hd:13 hp:120 \
                    tile:mons_statue_guardian
# please make sure this is the book with Tukima's Dance
KITEM:      8 = book of enchantments
KMONS:      9 = ice statue col:silver spells:blink_other;blink_other;blink_other;\
                    blink_other;blink_other name:이상한_기계 \
                    name_replace name_descriptor hd:13 hp:120 \
                    tile:mons_statue_guardian
KITEM:      9 = scroll of acquirement
:           set_feature_name("clear_rock_wall", "투명한 수정 거울")
:           set_feature_name("granite_statue", "이상한 기계")
:           set_feature_name("dry_fountain_blue", "반짝이는 분수")
:           set_feature_name("stone_wall", "검은 대리석 벽")
:           set_feature_name("floor", "대리석 바닥")
:           set_random_mon_list("dancing weapon")
:           wizlab_setup(_G, "투키마의 공방")
epilogue{{
            wizlab_milestone(_G, "투키마의 공방")
}}
MAP
                ccccccc
                ccmmmcc
            cccccm...mccccc
           cc...+.A.<.+...cc
          cc..cccm...mccc..cc
          cc+cccccm.mccccc+cc
          cc.cccccc+cccccc.cc
         cc...cccc...cccc...cc
         cm...mcm..1..mcm...mc
         cm...mcm.....mcm...mc
         cm.V.mcm.....mcm.V.mc
         cm...mcm.....mcm...mc
         cm...mcm..1..mcm...mc
         cc...cccc...cccc...cc
         ccc.cccccc+cccccc.ccc
          cc+ccccm...mcccc+cc
         ccm.mccm.....mccm.mcc
         cm.1..+...V...+..1.mc
        ccXm.mccm.....mccm.mYcc
      cccxccmlcccm...mcccrmccycc
      ccm.mcccLcccm.mcccRcccm.mcc
     ccm...mLLcccm...mcccRRm...mcc
    ccm..$..mcccm..?..mcccm..$..mcc
    cm..$8$..m+m...M...m+m..$9$..mc
    ccm..$..mcccm.....mcccm..$..mcc
     ccm...mcc ccm...mcc ccm...mcc
      ccmmmcc   ccmmmcc   ccmmmcc
       ccccc     ccccc     ccccc
ENDMAP

###############################################################################
# Eringya's Formal Garden
#
# Map was based on this image: http://upload.wikimedia.org/wikipedia/commons/5
#          /58/Plan_du_ch%C3%A2teau_et_des_jardins_de_Clagny_dessin%C3%A9_par_
#          Maraine_XVIIe_si%C3%A8cle.jpg (concat it back together to get the
#          full link).
#
# TODO: Needs vine-covered statues, rose-covered archway.
#       The map is too static. Some ideas by dpeg: loot behind plants or
#       bushes, so you'll have to cut/burn your way there. With catastrophic
#       effects, of course (plants turning into oklobs, fruits turning into
#       spores or (if possible) animated weapons called like the fruit etc.)
#       We could add a interesting little sub-game by having ballistos spawn
#       on their own - and these could be aggressive ballistos, too!
NAME:       wizlab_eringya
ORIENT:     encompass
TAGS:       no_item_gen no_monster_gen no_rotate allow_dup
MONS:       centaur col:green, centaur warrior col:lightgreen
MONS:       oklob plant
MONS:       stone golem name:덩굴로_덮인 name_adjective col:lightgreen \
            tile:mons_vine_golem
MONS:       toadstool
SUBST:      y = ct
COLOUR:     G = lightgrey
TILE:       G = dngn_statue_centaur
KFEAT:      / = stone_arch
KFEAT:      _ = altar_fedhas
MARKER:     A = feat:stone_arch
TILE:       c = wall_brick_vines
LROCKTILE:  wall_brick_vines
LFLOORTILE: floor_grass
ITEM:       any fixed theme book, any fixed level book, any magical staff / any rod w:2
{{

local grow_marker = TriggerableFunction:new { func = "wizlab_eringya_grow",
                                              repeated = true }

grow_marker:add_triggerer(DgnTriggerer:new { type="turn",
        delay_min=500, delay_max=800, })

lua_marker("_", grow_marker)
}}
:           set_border_fill_type("stone_wall")
:           wizlab_setup(_G, "에리냐의 정원")
:           set_feature_name("stone_arch", "장미들이 핀 아치")
:           set_feature_name("granite_statue", "장미덩굴 석상")
:           set_feature_name("stone_wall", "부스러진 돌벽")
:           set_feature_name("floor", "잔디 바닥")
:           set_random_mon_list ("oklob plant / centaur / centaur warrior")
epilogue{{
            wizlab_milestone(_G, "에리냐의 정원")
}}
MAP
ccccccccccccccccccccccccccccccccccccccccccccc
ctttttttttttttttttttttttttttttttttttttttttttc
ctttttttt...........................ttttttttc
ctttt.....tttttttttttt.tttttttttttt.....ttttc
cttt..tt.tttttttttt.......tttttttttt.tt..tttc
ct.1.tGt............<.T.A............tGt.1.tc
cttt..tt.tttttttttt.......tttttttttt.tt..tttc
ctttt.....tttttttttttt.tttttttttttt.....ttttc
ctttttttt...........................ttttttttc
cttttttttttttttttttttt.tttttttttttttttttttttc
ctttttttttyyyttttttty...ytttttttyyytttttttttc
ctttttttty...yttttty.....yttttty...yttttttttc
cttttttt..111.........T.........111..tttttttc
ctttttt...121.tttttc.....cttttt.121...ttttttc
cttttt.tty...yttttttcc+cctttttty...ytt.tttttc
ctttt.tttty.yttttttt.....ttttttty.ytttt.ttttc
cttt.tttttt.tttttttyd232eyttttttt.tttttt.tttc
cttt.tttttt..tttttttydefyttttttt..tttttt.tttc
ctttt.tttttt..tttttttytyttttttt..tttttt.ttttc
cttttt.tttttt..ttttttttttttttt..tttttt.tttttc
ctttttt.tttttt..tttttcccttttt..tttttt.ttttttc
cttttyyy/yyyttt..ttty...yttt..tttyyy/yyyttttc
cttty...1...yttt..ty..1..yt..ttty...1...ytttc
ctty...1T1...yttt....1T1....ttty...1T1...yttc
cttty...1...yttt..ty..2..yt..ttty...1...ytttc
cttttyyy/yyyttt..ttty...yttt..tttyyy/yyyttttc
ctttttt.tttttt..tttttcccttttt..tttttt.ttttttc
cttttt.tttttt..ttttttttttttttt..tttttt.tttttc
ctttt.tttttt..ttttt.G._.G.ttttt..tttttt.ttttc
cttt.tttttt..ttttt...4.4...ttttt..tttttt.tttc
cttt.ttttt..ttttt...........ttttt..ttttt.tttc
ctttt.ttty/ytttt....cc+cc....tttty/yttt.ttttc
cttttt.ty...yttttttcc...cctttttty...yt.tttttc
ctttttt./.1./.ttttcc..2..cctttt./.1./.ttttttc
cttttttty...yt...../.1T1../....ty...ytttttttc
cttttttttyyyttttttty.....ytttttttyyyttttttttc
ctttttttttttttttttttyytyytttttttttttttttttttc
ccccccccccccccccccccccccccccccccccccccccccccc
ENDMAP

###############################################################################
# Doroklohe's Forbidden Tomb
#
# It seems as if he practised box casting. I wonder what that's good for.
# Vault by dpeg. Thanks to Enne for box_glyph and to Jude for triggers.
#
# A bleach sun settles in the smog-stained sky
# Dismembered bodies stray in disarray
# Breakfast is served in the Manchester Morgue
# The beginning of a horrifying day
#                                    Impetigo 1992
#
# TODO: Tiles.
NAME:       wizlab_doroklohe
ORIENT:     encompass
TAGS:       no_item_gen no_monster_gen no_rotate allow_dup
LROCKCOL:   yellow
KFEAT:      ! = x
{{
function make_box(cenx, ceny, gly)
  for ox = -1, 1 do
    for oy = -1, 1 do
      local x = cenx + ox
      local y = ceny + oy
      if (x >= 1 and y >= 1 and x <= width() and y <= height()) then
        if (ox ~= 0 or oy ~= 0) then
          mapgrd[x][y] = gly;
        end
      end
    end
  end
end

function box_glyph(findgly, boxgly)
  local glyphs = {gly_points(findgly)}
  for i = 1, #glyphs, 2 do
    -- This is ugly.  gly_points should return 1-indexed coordinates.
    -- No longer ugly, as mapgrd is 0-indexed, like gly points.
    make_box(glyphs[i], glyphs[i+1], boxgly)
  end
end
}}
#           There are two layouts: round and grid.
#           Round has 19 boxes: 12 % and 6 * and 1 |
#           Grid has 21 boxes: b-u and |
#           Layout for grid can be regular or random.
#
:           local layout = crawl.random2(13)
:           if layout < 3 then
#           Preparation for the rare regular grid layout.
SUBST:      bcdefghijk = ., lmnopqrstu = .
SUBST:      B=b, C=c, D=d, E=e, F=f, G=g, H=h, I=i, J=j, K=k
SUBST:      L=l, M=m, N=n, O=o, P=p, Q=q, R=r, S=s, T=t, U=u
:           end
:           if layout > 9 then
#           First the round layout...
COLOUR:     _ = darkgrey
KFEAT:      _ = stone_wall
TILE:       _ = wall_stone_black_marked / wall_stone_dark w:4 / wall_stone_black_marked
SUBST:      bcdefghijk = ., lmnopqrstu = .
SUBST:      BCDEFGHIJK = ., LMNOPQRSTU = .
:           else
#           ...and now the grid layout.
SUBST:      _ = .
SUBST:      *=., %=.
SUBST:      B=b, C=c, D=d, E=e, F=f, G=g, H=h, I=i, J=j, K=k
SUBST:      L=l, M=m, N=n, O=o, P=p, Q=q, R=r, S=s, T=t, U=u
NSUBST:     b = 1:b / *:., c = 1:c / *:.
NSUBST:     d = 1:d / *:., e = 1:e / *:.
NSUBST:     f = 1:f / *:., g = 1:g / *:.
NSUBST:     h = 1:h / *:., i = 1:i / *:.
NSUBST:     j = 1:j / *:., k = 1:k / *:.
NSUBST:     l = 1:l / *:., m = 1:m / *:.
NSUBST:     n = 1:n / *:., o = 1:o / *:.
NSUBST:     p = 1:p / *:., q = 1:q / *:.
NSUBST:     r = 1:r / *:., s = 1:s / *:.
NSUBST:     t = 1:t / *:., u = 1:u / *:.
#           Of the outer boxes, three are damaged.
SHUFFLE:    bcdefghijklm
:           box_glyph('b', 'X')
:           box_glyph('c', 'Y')
:           box_glyph('d', 'Z')
SUBST:      bcd = .
NSUBST:     X = 1=. / *=!:20 .:1
NSUBST:     Y = 1=. / *=!:30 .:1
NSUBST:     Z = 1=. / *=!:50 .:1
#           For the other boxes, prepare same setup as for the
#           round layout (i.e. six *'s)
SHUFFLE:    nqru
SUBST:      efghijklmn = %
SUBST:      opqrstu = *
:           end
#           Monsters
#            0 Tomb set: zombies, mummies (weight *2)
#            1 Demons (if demons are added or reclassified, please change!)
#            2 Golems
#            TODO: 3 Holy set (for later)
:           local mrnd = crawl.random2(4)
:           if mrnd == 0 then
KMONS:      0% = clay golem / earth elemental / toenail golem w:1
KMONS:      * = stone golem / crystal golem / iron golem
KMONS:      | = electric golem
:           set_random_mon_list("clay golem / toenail golem w:1 / " ..
:                               "stone golem / crystal golem / iron golem")
:           elseif mrnd == 1 then
KMONS:      0% = smoke demon / ynoxinul / neqoxec / ice devil /\
                 chaos spawn / soul eater / sun demon
KMONS:      * = lorocyproca / reaper / shadow demon / tormentor / hellion /\
                blizzard demon / green death / balrug / cacodemon
KMONS:      | = hell sentinel / brimstone fiend / shadow fiend /\
                ice fiend / executioner
:           set_random_mon_list(
:               "smoke demon / ynoxinul / neqoxec / ice devil /" ..
:               "chaos spawn / soul eater / sun demon / " ..
:               "lorocyproca / reaper / shadow demon / tormentor / hellion")
:           else
KMONS:      0% = place:D:20 zombie / guardian mummy w:4
KMONS:      * = zombie / mummy priest w:6
KMONS:      | = ancient lich / greater mummy
:           set_random_mon_list("place:D:20 zombie / zombie / guardian mummy")
:           end
#           Loot.
#           0 = scrolls. 1 = armours. 2 = mixed.
:           local lrnd = crawl.random2(3)
:           if lrnd == 0 then
KITEM:      % = any scroll / any scroll q:2 w:3 / any scroll q:3 w:1
:           kitem("* = " .. dgn.good_scrolls)
KITEM:      | = scroll of acquirement ident:type / scroll of enchant weapon III q:2 ident:type /\
                scroll of blinking q:2 ident:type, scroll of fog q:2 ident:type, \
                scroll of holy word q:2 ident:type
:           elseif lrnd == 1 then
KITEM:      % = any armour
KITEM:      * = any good_item armour
KITEM:      | = cursed robe ego:resistance ident:type / cursed gold dragon armour
:           else
KITEM:      % = any / star_item w:2
KITEM:      * = star_item / superb_item w:2
KITEM:      | = superb_item
:           end
#           Boxification:
:           box_glyph('%', '!')
:           box_glyph('*', '!')
:           box_glyph('|', '!')
#           Intentionally colouring the floor glyphs of the central room only.
#           The squares beneath the boxes are to remain uncoloured.
NSUBST:     x = 1:. / 1 = x. / *:x
COLOUR:     ^ = darkgrey
TILE:       ^ = wall_stone_black_marked / wall_stone_dark w:4 / wall_stone_black_marked
COLOUR:     = = yellow
KFEAT:      ^ = stone_wall
COLOUR:     . = darkgrey / blue w:3 / cyan w:1
MARKER:     ! = lua:portal_desc {wall_drop=1}
MARKER:     = = lua:portal_desc {wall_drop=1}
TILE:       !x = wall_sandstone
{{
local box_marker = TriggerableFunction:new {
    func="wizlab_doroklohe_convert_boxes",
    repeated=true, data={phase=1} }

box_marker:add_triggerer(DgnTriggerer:new { type="turn",
    delay_min=500, delay_max=1000})

lua_marker("A", box_marker)
}}
:           set_feature_name("rock_wall", "기이한 암벽")
:           set_feature_name("stone_wall", "빽빽히 장식된 벽")
:           set_feature_name("floor", "거칠게 다듬어진 바닥")
:           wizlab_setup(_G, "도로크로흐의 무덤")
epilogue{{
            wizlab_milestone(_G, "도로크로흐의 무덤")
}}
MAP
^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
^________________...........________________^
^____________...................____________^
^_________........................._________^
^_______b....cc......d%d......ee....f_______^
^_____bB.....cC%.....dDd.....%Ee.....Ff_____^
^____bb...............................ff____^
^___.....................................___^
^__........%......o*.....*p......%........__^
^__.......nnN....oO.......Pp....Qqq.......__^
^_........................................._^
^_...mm...............................gg..._^
^_...mM..%......*.....|.....*......%..Gg..._^
^_...mm...............................gg..._^
^_........................................._^
^__.......uuU....tT.......Ss....Rrr.......__^
^__........%......t*.....*s......%........__^
^___.....................................___^
^____ll...............................hh____^
^_____lL.....kK%.....xxx.....%Ii.....Hh_____^
^_______l....kk......x0x......ii....h_______^
^_________...........xxx..........._________^
^____________...................____________^
^________________...........________________^
^^^^^^^^^^^^^^^^^^^^^^#^^^^^^^^^^^^^^^^^^^^^^
                ^____...____^
                ^__.......__^
                ^_...!+!..._^
                ^_.<.+A+.<._^
                ^_...!+!..._^
                ^__.......__^
                ^____...____^
                ^^^^^^^^^^^^^
ENDMAP

##############################################################################
# Cigotuvi's Fleshworks (by Mu.)
#
# Cigotuvi has mastered the art of manipulating flesh, and his lab is a
# living testament to this fact, seeming like the interior of some
# ghastly beast. The walls and floor are slick and membranous, pulsing to an
# unheard heartbeat and oozing thick, green ichor from every inch.
#
# The bulk of Cigotuvi's Fleshworks is devoted to cells that house test
# subjects in various stages of degeneration. Most are sickly humanoids; the
# others are ugly things, pulsating lumps and abominations.
#
# The central, circular chamber houses Cigotuvi's flesh golem.
# The Eastern half of his lab is dominated by a snaking passageway filled with
# pulsating lumps and the occasional ugly thing.
#
# The western half of his lab is full of abominations, including the terrible
# "Cigotuvi's Monster".
#
# TODO:     * door tile is *really* ugly, fix this?
#           * Cigotuvi's Monster needs a much bigger buff. Convert to unique?
NAME:       wizlab_cigotuvi
ORIENT:     encompass
TAGS:       no_item_gen no_monster_gen no_rotate allow_dup
KPROP:      . = w:1 bloody / w:15 nothing
KPROP:      ' = bloody / w:5 nothing
KMONS:      a = human name:고통으로_울부짖는 dbname:deformed_humanoid n_adj n_spe tile:mons_deformed_human / \
            human name:기형 dbname:deformed_humanoid n_adj n_spe tile:mons_deformed_human  / \
            human name:기이하게_변형된 dbname:deformed_humanoid n_adj n_spe tile:mons_deformed_human   / \
            human name:온_몸이_꼬인 dbname:deformed_humanoid n_adj n_spe tile:mons_deformed_human    / \
            human name:그로테스크한 dbname:deformed_humanoid n_adj n_spe tile:mons_deformed_human  / \
            human name:끔찍하게_변형된 dbname:deformed_humanoid n_adj n_spe tile:mons_deformed_human    / \
            human name:뒤틀린 dbname:deformed_humanoid n_adj n_noc tile:mons_deformed_human
KMONS:      b = elf name:고통으로_울부짖는 dbname:deformed_humanoid n_adj n_spe tile:mons_deformed_elf     / \
            elf name:기형 dbname:deformed_humanoid n_adj n_spe tile:mons_deformed_elf      / \
            elf name:기이하게_변형된 dbname:deformed_humanoid n_adj n_spe tile:mons_deformed_elf       / \
            elf name:온_몸이_꼬인 dbname:deformed_humanoid n_adj n_spe tile:mons_deformed_elf        / \
            elf name:그로테스크한 dbname:deformed_humanoid n_adj n_spe tile:mons_deformed_elf      / \
            elf name:끔찍하게_변형된 dbname:deformed_humanoid n_adj n_spe tile:mons_deformed_elf        / \
            elf name:장기가_튀어나온 dbname:deformed_humanoid n_adj n_spe n_noc tile:mons_deformed_elf
KMONS:      d = orc name:고통으로_울부짖는 dbname:deformed_humanoid n_adj n_spe tile:mons_deformed_orc     / \
            orc name:기형 dbname:deformed_humanoid n_adj n_spe tile:mons_deformed_orc      / \
            orc name:기이하게_변형된 dbname:deformed_humanoid n_adj n_spe tile:mons_deformed_orc       / \
            orc name:녹아내린듯한 dbname:deformed_humanoid n_adj n_spe tile:mons_deformed_orc        / \
            orc name:그로테스크한 dbname:deformed_humanoid n_adj n_spe tile:mons_deformed_orc      / \
            orc name:끔찍하게_변형된 dbname:deformed_humanoid n_adj n_spe tile:mons_deformed_orc        / \
            orc name:온_몸이_뒤틀린 dbname:deformed_humanoid n_adj n_noc tile:mons_deformed_orc
KMONS:      u = ugly thing / w:2 very ugly thing
KMONS:      U = very ugly thing
KMONS:      J = pulsating lump
KMONS:      x = small abomination
KMONS:      X = large abomination
KMONS:      e = generate_awake giant eyeball
SUBST:      " = J:1 / .:6
KMONS:      8 = col:red clay golem name:살점_골렘 name_replace \
            tile:mons_flesh_golem name_descriptor
KMONS:      M = col:mutagenic tentacled monstrosity name:시고트비의_괴물 \
            name_replace hp:250 tile:mons_cigotuvis_monster
MARKER:     ! = lua:fog_machine { \
                 pow_max = 10, delay_min = 10, delay_max = 40, \
                 size = 1, size_buildup_amnt = 10, \
                 size_buildup_time = 500, cloud_type = "mutagenic fog", \
                 excl_rad = -1 }
MARKER:     ? = lua:fog_machine { \
                 pow_max = 20, delay_min = 10, delay_max = 40, \
                 size = 5, size_buildup_amnt = 10, \
                 size_buildup_time = 25, cloud_type = "mutagenic fog", \
                 excl_rad = -1 }
LFLOORCOL:  magenta
LFLOORTILE: floor_nerves
LROCKTILE:  wall_flesh
TILE:       c = wall_flesh
TILE:       m = wall_transparent_flesh
TILE:       + = no_random dngn_fleshy_orifice
COLOUR:     m = yellow
COLOUR:     c+ = lightred
COLOUR:     W = green
:           set_feature_name("shallow_water", "끈적이는 체액")
:           set_feature_name("stone_wall", "힘줄이 선 살점")
:           set_feature_name("clear_rock_wall", "얇은 세포막")
:           set_feature_name("floor", "박동하는 바닥")
: lua_marker('+', props_marker {
:           door_description_noun="살점 사이의 구멍",
:           door_berserk_verb_open="당신은 %s%s을(를) 찢어냈다",
:           door_berserk_adjective="!",
:           door_berserk_verb_close="당신은 %s%s을(를) 움츠려 닫았다.",
:           door_noisy_verb_open="%s%s이(가) 쩍소리와 함께 갈라졌다!", 
:           door_noisy_verb_close="%s%s이(가) 쩍소리와 함께 움츠려져 닫혔다!", 
:           door_airborne_verb_open="아래로 손을 뻗어, %s%s을(를) 갈라냈다.",
:           door_airborne_verb_close="아래로 손을 뻗어, %s%s을(를) 움츠려 닫았디.",
:           door_verb_open="당신은 %s%s을(를) 갈라냈다.",
:           door_verb_close="당신은 %s%s을(를) 움츠려 닫았다."
:           })
KITEM:      $ = w:45 potion of mutation / w:5 potion of gain strength / \
            w:5 potion of gain dexterity / w:5 potion of gain intelligence / \
            w:1 potion of cure mutation
KITEM:      * = gold
KITEM:      % = amulet of resist mutation good_item
KITEM:      : = randbook disc:transmutation owner:Cigotuvi
KITEM:      ^ = book of transfigurations / randbook disc:transmutation
KITEM:      | = staff of death
KITEM:      l = scroll of summoning
NSUBST:     L = 1:| / *:l
:           wizlab_setup(_G, "시고트비의 생체 실험실")
:           set_random_mon_list("pulsating lump / small abomination / ugly thing / very ugly thing")
epilogue{{
            wizlab_milestone(_G, "시고트비의 생체 실험실")
}}
MAP
ccccccccccccccccccccccccccccccccccccccccccccc
ccLLLcccc.cccccccccccAccccccccccccWWWW.U:cccc
c.....c.....ccc...ccc+cccccccccccW.....uU^ccc
c.X.........cc.u...m...m..ccccccW.......uU:cc
c.M........cc...b..m.x.m.a.cccccW..........cc
c.X........cc...d..m...m.u..ccccW.........Wcc
c..........cc.cccccm...mc...ccccc........Wccc
c.....ccc+ccccc..cccc+cccccccccccc......Wcccc
ccLLLccc..ccccuuu.cm...m..d..ccccc+cccccccccc
cccccccc..ccc..!...m.x.m.a..cccccc""ccccccccc
cc$$cccc..ccc.uuu..m...m...ccccccc""cc**%**cc
c$$$$cc...cccc....cm...m.cccWccccc""c...cee.c
c$$$$+...cccccc..cccc+cccc...Wcccc""c...c...c
c$$$$c...ccccccccc.m...m.xx...Wccc""cmm+c+mmc
cc$ccccc+cccccccc..m.x.m...u..Wccc""c.......c
ccccccc...cccc...a.m...m....x.cc.c""c.......c
cccc.........c..b..m...mc....cc..c""c...!...c
c.xJ...ccc...cc..cccc+cccc..cc...c""c......cc
c...ccccccc+cccccc.m.....ccccJJJJc""+.....ccc
cc...ccccc'''cccc..m.....uccJJJJJc""ccccccccc
cc...ccc''''''cc.!.m.....<uc.....+"""""""""cc
c..cccc''''''''cc.......cuccc....cc"""""""""c
c..ccc'''''''''cccccc+cccccccccccccccccccc""c
c..cc''''''''''ccccc...ccccccc""""""""""""""c
c..cc''''''''''ccc.......cccc""""""""""""""cc
c..c'''''''''''cc.........ccc""cccccccccccccc
c..c+c+c+c+c+ccc...........cc""""""""""""""cc
c..cXcXcXcXcXccc...........ccc""""""""""""""c
c..cccccccccccc.....c.c.....cccccccccccccc""c
c...xJ........+......?......+"""""ccc"""""""c
c...xJ........c.....c.c.....c""""""c"""""""cc
ccmmmccccmmmcccc...........cccccc""c""ccccccc
c.....cc.....cccWW.......WWcc""""""c""""""ccc
cc.JJccccuuucccccWW..8..WWcc""""""ccc""""""cc
ccJJ.cccc.a.ccccccWWWWWWWccc""ccccccccccc""cc
cc.dccccccuuccccccccWWWccccc"""""""""""""""cc
ccccccccccccccccccccccccccccc"""""""""""""ccc
ccccccccccccccccccccccccccccccccccccccccccccc
ENDMAP

###############################################################################
# Iskenderun's Mystic Tower
#
# TODO: Fix up the tiles to make them pretty. It's really quite boring. I've
#       no idea how it could be improved or made interesting, but I think that
#       a few boring maps is fine - it means that you don't know exactly what
#       you're going to get.
NAME:       wizlab_iskenderun
ORIENT:     encompass
TAGS:       no_item_gen no_monster_gen no_rotate allow_dup patrolling
:           wizlab_setup(_G, "이스킨데룬의 신비의 탑")
MONS:       purple draconian / deep elf mage spells:iskenderun's_mystic_blast \
            / nothing
MONS:       statue tile:mons_statue_mage spells:iskenderun's_mystic_blast;orb_of\
            _destruction col:lightmagenta
KITEM:      D = star_item, star_item / superb_item, star_item
KITEM:      E = randbook disc:conjuration owner:Iskenderun spells:iskenderun's_mystic\
                _blast, randbook disc:conjuration owner:Iskenderun, any book
KITEM:      F = any book good_item, any book good_item, any book
KMONS:      DEF = purple draconian
COLOUR:     v = magenta
COLOUR:     Uc = lightmagenta
TILE:       c = wall_zot_magenta
TILE:       v = dngn_metal_wall_magenta
LFLOORTILE: floor_rough_magenta
LFLOORCOL:  magenta
MARKER:     R = lua:fog_machine { \
                pow_max = 10, delay_min = 10, delay_max = 40, \
                size = 15, cloud_type = "purple smoke" \
            }
SUBST:      R = .
KFEAT:      - = altar_sif_muna
KFEAT:      _ = altar_vehumet
SHUFFLE:    DEF
:           set_random_mon_list("purple draconian / deep elf mage")
epilogue{{
            wizlab_milestone(_G, "이스킨데룬의 신비의 탑")
}}
MAP
ccccccccccccccccccccccccccccccccccccccccccc
cAc..c.c.c.c.c.c.c.ccRcc.c.c.c.c.c.c.cc.cRc
cc.cc<c.c.c.c.c.c.c.c.c.c.c.c.c.c.c.c.cc.cc
c.c.ccc.............c.c..............cc.c.c
c.cc...................................cccc
cc<c......2.....................2......c.cc
c.cc.............v.vv.vv.v..............c.c
cc..............vvv.1R1.vvv..............cc
c.c............v.v.1vvv1.v.v............c.c
cc............vvvv1vvEvv1vvvv............cc
c.c..........v..v1vvv+vvv1v..v..........c.c
cc..........vv.v1vv.....vv1v.vv..........cc
c.c........v.vv1vv.......vv1vv.v........c.c
cccc.......vvv1vvv.......vvv1vvv.......cccc
cR.....2......RvD+...2...+1+R......2.....Rc
cccc.......vvv1vvv.......vvv1vvv.......cccc
c.c........v.vv1vv.......vv1vv.v........c.c
cc..........vv.v1vv.....vv11.vv..........cc
c.c..........v..v1vvv+vvv1v..v..........c.c
cc............vvvv1vvFvv1vvvv............cc
c.c............v.v.1vvv1.v.v............c.c
cc..............vvv.1R1.vvv..............cc
c.c..............v.vv.vv.v..............c.c
cc.c......2.....................2......c.cc
cccc...................................cccc
c.c.cc..............c.c..............cc.c.c
cc.cc.c.c.c.c.c.c.c.c.c.c.c.c.c.c.c.c.cc.cc
cRc.cc.c.c.c.c.c.c.ccRcc.c.c.c.c.c.c.cc.cRc
ccccccccccccccccccccccccccccccccccccccccccc
ENDMAP

###############################################################################
# Zonguldrok's Shrine
NAME:       wizlab_zonguldrok
ORIENT:     encompass
TAGS:       no_item_gen no_monster_gen no_rotate allow_dup
:           wizlab_setup(_G, "존굴드로크의 성지")
SUBST:      ? = txxx
SUBST:      ! = t..
SUBST:      G = G...
NSUBST:     Z = Z / .
MONS:       patrolling skeletal warrior
MONS:       generate_awake ancient lich  col:brown name:존굴드로크의_리치 name_replace \
            name_descriptor tile:mons_zonguldrok_lich
COLOUR:     t = darkgrey / lightgrey
TILE:       t = dngn_tree_dead
LFLOORCOL:  darkgrey
LROCKCOL:   darkgrey
NSUBST:     d = 1:e / *:d
ITEM:       any scroll / any potion / any book w:2
ITEM:       long sword unrand:sword_of_zonguldrok
{{
lua_marker("G", portal_desc { grave = 1 })
lua_marker("Z", portal_desc { wizard_grave = 1})
lua_marker("W", portal_desc { desc="존굴드로크의 비석",
        veto_fragmentation="veto", veto_disintegrate="veto", veto_shatter="veto" })

local zombies_appear_marker = TriggerableFunction:new {
    func = "wizlab_zonguldrok_spawn_zombies",
    repeated = true
}

local zombies_killed_marker = TriggerableFunction:new {
    data = {
        number_killed = 0,
        door_appeared = false
    },
    func = "wizlab_zonguldrok_killed_zombie",
    repeated = true
}

local wizard_appear_marker = TriggerableFunction:new {
    data = {
        awoke = false
    },
    func = "wizlab_zonguldrok_awaken_wizard",
    repeated = true,
    props = {listen_to_slaves = true, only_at_slave = true }
    }

wizard_appear_marker:add_triggerer(DgnTriggerer:new {
    type = "player_move" })

zombies_appear_marker:add_triggerer(DgnTriggerer:new {
    type = "turn",
    delay_min = 200,
    delay_max = 300
})

zombies_killed_marker:add_triggerer(DgnTriggerer:new {
    type = "monster_dies",
    target = "any"
})

lua_marker("M", zombies_appear_marker)
lua_marker("D", zombies_killed_marker)
lua_marker("L", Triggerable.synchronized_markers(wizard_appear_marker))

}}
:           set_feature_name("granite_statue", "이름 모를 비석")
:           set_feature_name("floor", "땅")
SUBST:      M = x
SUBST:      D = c
COLOUR:     W = yellow
TILE:       W = dngn_gravestone_ornate
TILE:       G = dngn_gravestone
SUBST:      W = G
SUBST:      LZ = .
LFLOORTILE: floor_tomb
LROCKTILE:  wall_hall_darkgray
TILE:       c = wall_stone_gray
:           set_random_mon_list("human zombie / skeletal warrior")
epilogue{{
            wizlab_milestone(_G, "존굴드로크의 성지")
}}
MAP
M   xxxxxxxxxxxxxxxxxxx
   xx?????????????????xx
  xx?!!!!!!!!!!!!!!!!!?xx
 xx?!!.G.G.G.G.G.G.G.!!?xx
xx?!!.G.G.G.G.G.G.G.G.!!?xx
x?!!.G.G.G.G.G.G.G.G.G.!!?x
x?!.G.G.GcccccccccG.G.G.!?x
x?!G.G.G.c.ddcdd.c.G.G.G!?x
x?!.G.G.ccdd.c.ddcc.G.G.!?x
x?!G.Gcccddcc+ccddcccG.G!?x
x?!.G.c.dd.cLLLc.dd.c.G.!?x
x?!G.Gc$.ccc...ccc.$cG.G!?x
x?!.G.c$$c..ZZZ..c$$c.G.!?x
x?!G.Gcccc..ZWZ..ccccG.G!?x
x?!.G.c..+..ZZZ..+..c.G.!?x
x?!G.Gc.1ccc...ccc1.cG.G!?x
x?!.G.c..1.c...c.1..c.G.!?x
x?!G.Gccc.1ccccc1.cccG.G!?x
x?!.G.G.cc.1.c.1.cc.G.G.!?x
x?!G.G.G.c...c...c.G.G.G!?x
x?!.G.G.GccccDccccG.G.G.!?x
x?!!.G.G.G.GV.VG.G.G.G.!!?x
xx?!!.G.G.G..<..G.G.G.!!?xx
 xx?!!.G.G.G.A.G.G.G.!!?xx
  xx?!!!!!!!!!!!!!!!!!?xx
   xx?????????????????xx
    xxxxxxxxxxxxxxxxxxx
ENDMAP

###############################################################################
# Wucad Mu's Monastery
#
# TODO: I think this is done, apart from not having "alien"-esque statue tiles.
NAME:       wizlab_wucad
ORIENT:     encompass
TAGS:       no_item_gen no_monster_gen no_rotate allow_dup
SUBST:      T = t.
KMONS:      8 = orange crystal statue name:우카드_무_석상 n_rpl n_the \
                hd:20 hp:300 tile:mons_wucad_mu_statue
KITEM:      8 = staff of channeling unrand:staff_of_wucad_mu, book of enchantments
COLOUR:     =c = darkgrey
COLOUR:     t = lightred / red w:3
TILE:       t = dngn_tree_lightred / dngn_tree_red w:4 / dngn_tree_yellow w:4
# This used to specify dngn_statue_troll as the only middle case, but that tile
# no longer exists.
TILE:       G = dngn_granite_statue w:1 / \
                dngn_statue_angel w:1 / \
                dngn_statue_imp w:1 / \
                dngn_statue_ancient_evil w:1 / \
                dngn_statue_ancient_hero w:1
COLOUR:     AG = red
COLOUR:     < = lightred
TILE:       c = dngn_stone_dark
LFLOORTILE: floor_pebble_white
KFEAT:      ' = open_door
KFEAT:      1234567 = .
KMONS:      1234567 = nothing
LROCKCOL:   darkgrey
ITEM:       potion of experience, manual of fighting / manual of armour / manual of \
            spellcasting, manual of staves / manual of fighting / manual of dodging,\
            potion of gain dexterity / potion of gain strength / potion of gain \
            intelligence
LFLOORCOL:  white
KMONS:      F = human name:수도승 n_suf col:white hd:10 dur:2 sum:shadow_creatures \
            nas:old_memories tile:mons_human_monk seen / deep troll name:수도승 n_suf \
            dur:2 seen sum:shadow_creatures nas:old_memories tile:mons_deep_troll_monk \
            / iron troll name:수도승 n_suf col:white dur:2 sum:shadow_creatures \
            nas:old_memories seen tile:mons_iron_troll_monk w:5
{{
dgn.persist.wucad_mu_died = false

local function trigfn (spot)
  return Triggerable.synchronized_markers(function_at_spot(
            "wizlab_wucad_msgfn",
            { spot=spot, trig = false}, true, { only_at_slave = true,
              listen_to_slaves = true }))
end

lua_marker('1', trigfn(1))
lua_marker('2', trigfn(2))
lua_marker('3', trigfn(3))
lua_marker('4', trigfn(4))
lua_marker('5', trigfn(5))
lua_marker('6', trigfn(6))
lua_marker('7', trigfn(7))


local summon_marker = TriggerableFunction:new ({
    func="wizlab_wucad_summon_monks",
    repeated=true })

summon_marker:add_triggerer(DgnTriggerer:new {type="turn", delay_min=150,
    delay_max=400 })

local wucad_marker = TriggerableFunction:new ({
    func="callback.wizlab_wucad_wucad_died",
    repeated=false })

wucad_marker:add_triggerer(DgnTriggerer:new {type="monster_dies",
    target="우카드 무 석상"})

lua_marker('A', summon_marker)
lua_marker('A', wucad_marker)

}}
MARKER:     G = lua:props_marker {veto_fragmentation="veto", veto_disintegrate="veto", \
                                  veto_shatter="veto" }
MARKER:     = = lua:props_marker {veto_fragmentation="veto", veto_disintegrate="veto", \
                                  veto_shatter="veto" }
MARKER:     + = lua:props_marker {veto_fragmentation="veto", veto_disintegrate="veto", \
                                  veto_shatter="veto" }
MARKER:     ' = lua:props_marker {veto_fragmentation="veto", veto_disintegrate="veto", \
                                  veto_shatter="veto" }
MARKER:     c = lua:props_marker {veto_fragmentation="veto", veto_disintegrate="veto", \
                                  veto_shatter="veto" }
MARKER:     t = lua:props_marker {veto_fragmentation="veto", veto_disintegrate="veto", \
                                  veto_shatter="veto", veto_fire="veto" }
:           wizlab_setup(_G, "우카드 무의 수도원")
:           set_border_fill_type("tree")
:           set_random_mon_list("shadow / phantom w:1 / flayed ghost w:1 / hungry ghost w:1")
:           set_feature_name("tree", "단풍나무")
:           set_feature_name("stone_wall", "부스러진 암벽")
:           set_feature_name("granite_statue", "기이한 석상")
:           set_feature_name("floor", "오래된 지면")
epilogue{{
            wizlab_milestone(_G, "우카드 무의 수도원")
}}
MAP
tttttttttttttttttttttttttttttttttttttttt
tttttttttttttttttttttttttttttttttttttttt
ttttttttttttttTTTTTTTttttttttttttttttttt
ttttttTTTTTTTT.......TTTTTTTTTTTTttttttt
tttttT.........ccccc.............Ttttttt
ttttT.........cc777cc.............Tttttt
tttT.......cccc77777cccc...........Ttttt
ttT.......cc..cc777cc..cc......ccc..Tttt
ttT......cc....cc+cc....cc....cc6cc..Ttt
ttT.....Tc....G.....G....cT..cc6U6cc.Ttt
ttT.....ccG.............Gcc...cc6cc..Ttt
ttT......+.......8.......+.....c+c...Ttt
ttT.....ccG.............Gcc..........Ttt
ttT.....Tc....G.....G....cT..........Ttt
ttT.c..cTcc.............cccT.........Ttt
ttTcc++ccccccc.......ccccccc+cc......Ttt
ttcc5555cccdeccc+++cccfgccc444cc.....Ttt
ttc555555c....c.....c....c44444c....Tttt
ttc555555cc.............cc44444cc..ctttt
ttc555555ccccccccccccccccccc+cccc++ccttt
ttcc5555ccc.............ccc222cc3333cctt
tttccccccc...............+22222+33333ctt
ttttttttccc..ccc+++ccc..ccc222cc3333cctt
tttttttttcccccTT111Ttccccccccccccccccttt
ttttttttttttttT11111Tttttttttttttttttttt
tttttttttttT.........TTTTTTttttttttttttt
tttccccctttT.....G........Ttcccccttttttt
ttcc...cctT..............Ttcc...cctttttt
ttc..A..'..................'..<..ctttttt
ttcc...cctT.....TTTT.....Ttcc...cctttttt
tttccccctttT...TTttTT....Tttcccccttttttt
ttttttttttttTTTTttttTTTTTttttttttttttttt
tttttttttttttttttttttttttttttttttttttttt
tttttttttttttttttttttttttttttttttttttttt
ENDMAP

################################################################################
# The Roulette of Golubria (by HangedMan.)
# Golubria's fascination with translocations led him to warp out a sub-realm
# of the Abyss into his own translocations playground, with some nasty and wild
# security systems built in to defend his many assorted possessions.
#
# New explanation of the teleporter transformations: Three teleporter sets of
# four rooms each, diagonal and (default) clockwise orthogonal rotated, and
# (default) counter-clockwise orthogonal not. Each set's outward
# teleporters (would) always rotate the same amount of rooms per direction.
# However, the center's thirteenth main teleporter pair slightly distorts this,
# which makes it good and difficult to anticipate landing in the center
# even when deciphering this (horribly complicated) source.
NAME:    wizlab_golubria
TAGS:    no_monster_gen no_item_gen allow_dup no_pool_fixup
ORIENT:  encompass
LFLAGS:  no_tele_control
KMONS:   1 = spatial vortex w:8 / blink frog / insubstantial wisp w:2 / nothing w:5
KMONS:   2 = deep elf summoner w:5 ; dart ego:dispersal w:20 | dart w:5 | nothing / \
             rakshasa w:5 ; dart ego:dispersal w:20 | dart w:5 | nothing / \
             boggart ; dart ego:dispersal w:20 | dart w:5 | nothing /  \
             wizard ; dart ego:dispersal w:20 | dart w:5 | nothing
KMONS:   3 = rakshasa w:5 ; whip ego:distortion | whip ego:vorpal w:6 | dagger \
             ego:distortion | dagger ego:vorpal w:6 | spear ego:distortion | \
             spear ego:vorpal w:6 . dart ego:dispersal / \
             boggart w:5 ; whip ego:distortion | whip ego:vorpal w:6 | dagger \
             ego:distortion | dagger ego:vorpal w:6 | spear ego:distortion | \
             spear ego:vorpal w:6 . dart ego:dispersal / \
             wizard ; whip ego:distortion | whip ego:vorpal w:6 | dagger \
             ego:distortion | dagger ego:vorpal w:6 | spear ego:distortion | \
             spear ego:vorpal w:6 . dart ego:dispersal
# Disclaimer: the following three sets are all Abyss spawns.
KMONS:   04 = large abomination w:40 / small abomination w:40 / skeleton w:40 / \
              chaos spawn w:5 / soul eater / hellwing / orange demon / \
              rotting devil / hell hound / red devil w:5 / lesser demon w:40 / \
              flying skull / freezing wraith / octopode w:5 / base draconian w:5 / \
              ugly thing w:30 / fire elemental w:5 / air elemental w:5
KMONS:   95 = ynoxinul / smoke demon / sun demon w:5 / sixfirhy / efreet w:5 / \
              hell knight w:5 / demonic crawler / very ugly thing w:5 / \
              unseen horror / vapour w:5 / stone golem w:5 / \
              guardian serpent / naga mage w:5 / brain worm / eidolon w:5 / \
              shadow wraith / anaconda skeleton w:5 / flaming corpse / \
              great orb of eyes w:5 / giant orange brain w:5
KMONS:   6 = executioner w:5 / balrug w:5 / cacodemon / shadow demon / hellion w:5 / \
             tormentor w:5 / lorocyproca w:5 / blizzard demon / green death / \
             reaper / lich w:5 / angel / death drake w:5 / \
             tentacled monstrosity / draconian shifter w:5 / profane servitor w:5
KMONS:   7 = eldritch tentacle
KMONS:   8 = silver statue
# Translocations items, abyss escape aides, shrouds, and an immeadiate aide.
KITEM:   d = scroll of blinking / scroll of teleportation q:2 / \
             wand of teleportation w:5 / any scroll w:1
KITEM:   e = ring of teleport control w:9 / ring of teleportation w:20 / any ring w:1
KITEM:   f = book of spatial translocations w:5 / book of the warp w:5 / \
             manual of translocations w:5 / randbook disc:translocation numspells:5 / \
             randbook disc:translocation owner:Golubria numspells:7 \
             spells:shroud_of_golubria|passage_of_golubria
KITEM:   g = ring of flight / potion of flight q:2 / \
             wand of digging w:5 / lantern of shadows / \
             potion of speed w:5 / scroll of fog / potion of porridge / \
             any wand w:1 / any potion w:1 / any scroll w:1
KITEM:   ! = pair of boots ego:flying w:9 / pair of boots ego:running w:1 / \
             pair of boots ego:stealth w:9 / pair of boots w:1
KITEM:   $ = wand of disintegration
KITEM:   % = any level:3, any level:3 / nothing
KITEM:   * = star_item / any w:5, any w:5 / nothing
KITEM:   | = cloak good_item w:75 / randart cloak w:95 / cloak unrand:cloak_of_flash / \
             cloak unrand:cloak_of_starlight / cloak unrand:cloak_of_the_thief
KFEAT:   - = rock_wall
KFEAT:   N = iron_grate
KFEAT:   ~ = deep_water
KFEAT:   D = permarock_wall
KFEAT:   ^ = known teleport trap
KFEAT:   8_ = altar_lugonu
KFEAT:   OPQRSTUVWXYZ&{( = teleporter
KFEAT:   opqsrtuvwxyz@}) = floor
KPROP:   a"_~ = no_rtele_into
MARKER:  ^ = lua:fog_machine { cloud_type = "translocational energy",  \
             pow_max = 15, delay_min = 30, delay_max = 60, size_min = 2, size_max = 3 }
SHUFFLE: qS?tViwYJzOl / tViwYJzOKqS? / wYJzOKqS?tVi / zOKqS?tViwYJ
SHUFFLE: y&KpRmsUHvXj / vXjy&KpRmsUH / sUHvXjy&KpRm / pRmsUHvXjy&K
SHUFFLE: SMViYJOK / MSiVJYKO, &KRmUHXj / K&mRHUjX, QmThWIZk / mQhTIWkZ
NSUBST:  @ = 1:@ / 1:P / 1:L / 1:<, 4 = 4:5 / 4:4, ` = 4:d / 3:| / 1:|% / 1:$ / 4:% / *:`
NSUBST:  a = 1:_ / 1:_" / *:a, + = 2:+ / *:c, ( = 1:} / 1:( / *:<
NSUBST:  [ = 2:< / *:d, 2 = 1:66699 / 1:66993 / 2:9 / 1:993 / 1:92 / *:2
NSUBST:  d = 2:* / 2:e / 2:f / 3:g / 1:eegg!!% / 1:gg% / 3:%
NSUBST:  7 = 1:7 / 1 = 7:4 `:6 / *:`, A = 1:) / 1:< / 1:{ / 1:A
SUBST:   h = O, H = P, i = Q, I = R, j = S, J = T
SUBST:   k = U, K = V, l = W, L = X, m = Y, M = Z, ? = &
SUBST:   a = ':70 -:40 ccCC~, F = ":70 -:40 ccCC~, E = nccCC, c = cccb, C = cb
SUBST:   B = ccbn..., e = de, % = %%%%g., * = **%, 4 = 444'', 5 = 5555544''
COLOUR:  n = lightgreen
COLOUR:  c = white
COLOUR:  OPQRSTUVWXYZ&{( = warp
COLOUR:  opqrstuvwxyz@}) = gold
COLOUR:  D = darkgrey
LFLOORCOL: green
FTILE:   '"GbcaFn45_+ = floor_demonic_green
FTILE:   .012369AB< = floor_rough_green / floor_pebble_green w:15
FTILE:   `~78N = floor_demonic_green / floor_pebble_green / floor_rough_green w:5
FTILE:   defg!$%*| = floor_rough_green
LROCKTILE: wall_zot_green
TILE:    b = dngn_crystal_green / dngn_crystal_cyan w:5
TILE:    c = dngn_stone_wall_green / dngn_stone_wall_cyan w:5
TILE:    n = dngn_transparent_stone_green / dngn_transparent_stone_cyan w:5
TILE:    D = dngn_dimension_edge
FTILE:   OPQRSTUVWXYZ&{( = floor_rough_green
TILE:    opqrstuvwxyz@}) = floor_black_cobalt

{{
-- This isn't the prettiest way of handling the teleporters, but it guarantees
-- they behave exactly the same way as in HangedMan's original version. All I've
-- done is remove some massive code duplication. [evilmike]

local teleporter_glyph = "OPQRSTUVWXYZ&?{("
local teleport_spot_glyph = "opqrstuvwxyz@!})"
local tele_marker = {}

for i = 1, #teleporter_glyph do
    tele_marker[i] = TriggerableFunction:new {
                     func="teleporter_golubria_switch_fn",
                     data = {teleport_spot=i},
                     repeated=true }
    tele_marker[i]:add_triggerer(DgnTriggerer:new { type="player_move" })
    lua_marker(teleporter_glyph:sub(i,i), tele_marker[i])
    lua_marker(teleport_spot_glyph:sub(i,i), portal_desc { teleport_spot=i})
end

set_random_mon_list([[rakshasa w:15 / deep elf summoner w:15 / wizard w:15 / spatial vortex w:15 /
                    boggart w:5 / blink frog w:5 / shadow wraith w:5 / unseen horror /
                    ynoxinul / neqoxec / chaos spawn / giant orange brain /
                    large abomination w:20 / small abomination w:20 / skeleton w:40 /
                    hellwing w:5 / smoke demon w:5 /
                    skeletal warrior w:5 / hell hound w:5 / very ugly thing w:5 /
                    golden eye w:5 / executioner w:2 / shadow demon w:2 / tormentor w:2 / ]])

set_border_fill_type('permarock_wall')
set_feature_name("permarock_wall", "차원의 가장자리")
set_feature_name("green_crystal_wall", "수정 벽")
wizlab_setup(_G, "골루브리아의 룰렛")
}}

epilogue{{
            wizlab_milestone(_G, "골루브리아의 룰렛")
}}
MAP
               DDDDDDDDDD   DDDDDDDDDD
    DDDDDD    DDccEEEcccDDDDDcccEEEccDD    DDDDDD
  DDDccccDDDDDDcc..o...ccccccc...p..ccDDDDDDccccDDD
  DEEc.lccccccccQ......mcaaacR......MccccccccS.cEED
 DDEz....ccaaaan........naaan........naaaacc....qEDD
 Dcc......caaa"n........n"a"n........n"aaac......ccD
 Dc....B..cnaaan...BBB..naaan..BBB...naaanc..B....cD
 DcO..BBB..n"aac...BB2..caaac..2BB...caa"n..BBB..?cD
 Dcc...BB..ccaacc...B..0caaac0..B...ccaacc..BB...ccD
 DDcc....2.0caaacc1....ccaaacc....1ccaaac1.2....ccDD
  Dcccc...[.ccaaacc1.[.caaaaac.[.1ccaaacc1[...ccccD
  Dcaannc11..ccaaacc...caaaaac...ccaaacc...0cnnaacD
  Dcaaa"ccc...caaaacc.ccaaaaacc.ccaaaac...ccc"aaacD
  Dcaaaaaacc..cFaaaFc+cFaaaaaFc+cFaaaFc..ccaaaaaacD
 DDca"aaaaaccc+FFaFF'F'FaaaaaF'F'FaaFF+cccaaaaa"acDD
DDccnnnccaaaaFF'FFF'FFFFFF4FFFFFF'FFF'FFaaaaccnnnccDD
DccK....ccaaaaFF'F'FFFFFF'G'FFFFFF'F'FFaaaacc....TccD
Dc.......ccaaaaFF4F'FF'F'FFF'F'FF'F4FFaaaacc.......cD
DE.......1ccaaFF'FGF''C'C'F'C'C''FGF'FFaacc1.......ED
DEy...BB..1ccFF'F'FFCCCCCC(CCCCCCFF'F'FFcc1..BB...rED
DE....BBB...cc'FFF'Cn'C^'CnC'^C'nC'FFF'cc...BBB....ED
Dc....B2..[..+FFFF'C'@C'``n``'C@'C'FFFF+..[..2B....cD
Dc..........cc'FF'CCCC`````````CCCC'FF'cc..........cD
Dcc&....0ccccFFFFF'C^'`````````'~C'FFFFFcccc0....hccDDDDDDDD
DDccnnncccaaaaaFF'CC'````NNN````'CC'FFaaaaacccnnnccDDDnnnnnD
 Dcaa"aaaaaaaaFF'F'CC'``NN7NN``'CC'F'FFaaaaaaaa"aacDDDnA.AnD
 DcaaaaaaaaaaaF4GFF(nn``N787N``nn(FFG4FaaaaaaaaaaacDDDn...nD
 Dcaa"aaaaaaaaFF'F'CC'``NN7NN``'CC'F'FFaaaaaaaa"aacDDDnA.AnD
DDccnnncccaaaaaFF'CC'````NNN````'CC'FFaaaaacccnnnccDDDnnnnnD
Dcck....0ccccFFFFF'C^'`````````'^C'FFFFFcccc0....UccDDDDDDDD
Dc..........cc'FF'CCCC`````````CCCC'FF'cc..........cD
Dc....B2..[..+FFF'CC'@C'`'n'`'C@'C'FFFF+..[..2B....cD
DE....BBB...cc'FFF'Cn'C^'CnC'^C'nC'FFF'cc...BBB....ED
DEx...BB..1ccFF'F'FFCCCCCC(CCCCCCFF'F'FFcc...BB...sED
DE.......1ccaaFF'FGF''C'C'F'C'C''FGF'FFaacc1.......ED
Dc.......ccaaaaFF4F'FF'F'FFF'F'FF'F4FFaaaacc1......cD
DccZ....ccaaaaaF'F'FFFFFF'G'FFFFFF'F'Faaaaacc....HccD
DDccnnnccaaaaFF'FFF'FFFFFF4FFFFFF'FFF'FFaaaaccnnnccDD
 DDca"aaaaaccc+FFaFF'F'FaFFFaF'F'FaaFF+cccaaaaa"acDD
  Dcaaaaaacc..cFaaaFc+cFaaaaaFc+cFaaaFc..ccaaaaaacD
  Dcaaa"ccc...caaaacc.ccaaaaacc.ccaaaac...ccc"aaacD
  Dcaannc0...ccaaacc...caaaaac...ccaaacc..11cnnaacD
  Dcccc...[1ccaaacc1.[.caaaaac.[.1ccaaacc.[...ccccD
 DDcc....2.1caaacc1....ccaaacc....1ccaaac0.2....ccDD
 Dcc...BB..ccaacc...B..0caaac0..B...ccaacc..BB...ccD
 DcJ..BBB..n"aac...BB2..caaac..2BB...caa"n..BBB..VcD
 Dc....B..cnaaan...BBB..naaan..BBB...naaanc..B....cD
 Dcc......caaa"n........n"a"n........n"aaac......ccD
 DDEw....ccaaaan........naaan........naaaacc....tEDD
  DEEc.Yccccccccj......XcaaacI......Wcccccccci.cEED
  DDDccccDDDDDDcc..v...ccccccc...u..ccDDDDDDccccDDD
    DDDDDD    DDccEEEcccDDDDDcccEEEccDD    DDDDDD
               DDDDDDDDDD   DDDDDDDDDD
ENDMAP

###############################################################################
# Random and semi-random Wizard vaults
###############################################################################

###############################################################################
# Chambers of the Cloud Mage (by Mu.)
#
# TODO: Needs an "ocean of clouds" tiles, and fluffy "cotton wool" cloud tiles.
#       Most everything else is done, except redescriptions.
NAME:       wizlab_cloud
ORIENT:     encompass
TAGS:       no_item_gen no_monster_gen no_rotate allow_dup
{{
wizname = crawl.make_name()

kmons ("    1 = patrolling Cloud Mage dbname:Cloud_Mage name:구름의_마법사 name_replace"
         .. " spells:mephitic_cloud;freezing_cloud;poisonous_cloud;"
         .. "airstrike;blink_range;blink_range hd:20 hp:150 ;"
         .. " quick blade ego:freezing | dagger ego:speed | dagger ego:electrocution"
         .. " | dagger ego:freezing . robe unrand:robe_of_clouds")
}}
MARKER:     1 = lua:MonPropsMarker:new {description="이 기이한 마법사는 " \ 
            .. "구름에 대한 모든 것을 터득하였으며, 그의 신비하면서도 " \ 
            .. "능수능란한 손놀림을 보고 있자니 왠지 모르게 불길한 예감이 든다.\n"}
KMONS:      * = w:5 vapour / w:20 air elemental
KMONS:      v = twister
SUBST:      . = .:120 *
MARKER:     ! = lua:fog_machine { \
                pow_max = 10, delay_min = 10, delay_max = 40, \
                size = 2, size_buildup_amnt = 5, \
                size_buildup_time = 25, cloud_type = "grey smoke", \
                colour = "white", name = "white fluffiness" \
            }
KITEM:      % = w:20 potion of flight / \
            potion of magic / potion of speed / potion of resistance / \
            gold / potion of agility / potion of brilliance
KITEM:      ? = randbook disc:air
KITEM:      $ = wand of lightning / staff of air / ring of flight /\
            gold / scroll of silence / ring of teleport control / \
            quick blade / ring of evasion / ring of see invisible / \
            book of clouds / any book w:15 / any good_item / any w:20
KFEAT:      v# = deep_water
NSUBST:     $ = 2:? / *:$, # = 5:v / 4 = v## / *:#
SUBST:      $ = $%
COLOUR:     +<bo = white
LFLOORCOL:  lightblue
LFLOORTILE: floor_rough_lightblue
TILE:       o = dngn_transparent_stone_white
TILE:       b = dngn_crystal_white
COLOUR:     wWv# = silver
:           set_border_fill_type('open_sea')
:           set_random_mon_list("vapour / air elemental")
:           wizlab_setup(_G, "구름의 마법사의 방") 
:           set_feature_name("floor", "축축한 바닥")
:           set_feature_name("green_crystal_wall", "흰 수정 벽")
:           set_feature_name("clear_permarock_wall", "투명한 수정 벽")

epilogue{{
            wizlab_milestone(_G, "구름의 마법사의 방") 
}}
#           Map is ugly because border_fill_type doesn't allow for (yet)
#           recolouring.
MAP
#####################################################################
#####################################################################
#####################################################################
#####################################################################
#####################################################################
#####################################################################
#####################################################################
#####################################################################
#####################################################################
#####################################################################
#####################oooooooooooooooooooooooooooooo##################
###################ooo............................ooo################
#################ooo...wwwwwwwwwwwwwwwwwwwwwwwwww...ooo##############
################oo...wwwwwwwbbbbbbbbbbbbbbbbbbbbwww...oo#############
################o..wwwwwwwbbbbbbbbbbbbbbbbbbbbbbwwwww..o#############
###############oo.wwwwwwbbbbbbbb..b..b..b..b..bbwwwwww.oo############
###############o..wwwwwbb.....b.!............!.bwwwwww..o############
##############oo.wwwwwwbb.$$$.b................bwwwwwww.oo###########
##############o..wwwwwbbb.$$$.b................bWWWWWWW..o###########
##############o..wwwwwbbb.$$$.+...1............+.........o###########
##############o..wwwwwbbb.$$$.b................bWWWWWWW..o###########
##############oo.wwwwwwbb.$$$.b................bwwwwwww.oo###########
###############o..wwwwwbb.....b.!............!.bwwwwww..o############
###############oo.wwwwwwbbbbbbbb..b..b..b..b..bbwwwwww.oo############
################o..wwwwwwwbbbbbbbbbbbbbbbbbbbbbbwwwww..o#############
################oo...wwwwwwwbbbbbbbbbbbbbbbbbbbbwww...oo#############
#################oo....wwwwwwwwwwwwwwwwwwwwwwwwww...ooo##############
##################oooo............................ooo################
#####################bbb.......bbbbbbbbbbbbbbbbbbbb##################
#####################oo.......oo#####ooo#############################
####################oo.......oo#####oo.oo############################
###################oo.......oo#####oo.!.oo###########################
###################o!.....!ooo####oo.....ooo#########################
###################ooo.......ooo#oo........oo########################
####################oooo.......ooo..........oo#######################
#######################ooo!.....ooo..........o#######################
########################o.....!ooobb+bb......oo######################
#######################ooo......obb...bb!.....o######################
########################o......obb.....bb.....oo#####################
#######################ooo......b.......b......o#####################
#######################o.!.....ob.<.A.<.b......o#####################
#######################o.b......b.......bo...!oo#####################
#######################o.bo.....bb.....bb......o#####################
#######################b+bbbb....bb...bb!.....oo#####################
#######################o....bo...!bbbbb......oo######################
#######################o.%%.bo.............oob#######################
#######################o.%%.boo...........bbbbbbbbb##################
#######################o....b#o..........!...+....o##################
#######################ooooob#oo........ooooob.%%.o##################
###############################oo......oo####b.%%.o##################
################################oo.!..oo#####b....o##################
#################################oooooo######booooo##################
#####################################################################
#####################################################################
#####################################################################
#####################################################################
#####################################################################
#####################################################################
#####################################################################
#####################################################################
#####################################################################
ENDMAP

###############################################################################
# Halls of the Hellbinder (by Mu.)
NAME:       wizlab_demon
ORIENT:     encompass
TAGS:       no_item_gen no_monster_gen no_rotate no_hmirror no_vmirror allow_dup
LFLOORCOL:  red
LROCKCOL:   red
LFLOORTILE: floor_pebble_lightred
COLOUR:     c = darkgrey
COLOUR:     -|/\o[]CDEFGHIJKLMNOPQRSTUVWXYZu = yellow
KFEAT:      -|/\o[]CDEFGHIJKLMNOPQRSTUVWXYZu = floor
KFEAT:      _ = altar_makhleb
KPROP:      ' = bloody / nothing
KPROP:      _ = bloody
TILE:       c = wall_stone_dark
# symbols roughly corresponding to sigil shape
FTILE:      - = sigil_straight_E_W
FTILE:      | = sigil_straight_N_S
FTILE:      / = sigil_straight_NE_SW
FTILE:      \ = sigil_straight_NW_SE
FTILE:      o = sigil_circle
FTILE:      T = sigil_cross
FTILE:      Y = sigil_Y
FTILE:      ] = sigil_Y_left
FTILE:      [ = sigil_Y_right
FTILE:      O = sigil_rhombus
FTILE:      Q = sigil_algiz_left
FTILE:      R = sigil_algiz_right
FTILE:      S = sigil_sharp_E_NE
FTILE:      Z = sigil_sharp_W_SW
FTILE:      K = sigil_straight_E_NE_SW
FTILE:      X = sigil_fourway
FTILE:      Uu = sigil_Y_inverted
FTILE:      V = sigil_Y_leftleaning
FTILE:      W = sigil_Y_rightleaning
# remaining sigil patterns
FTILE:      C = sigil_curve_S_E
FTILE:      D = sigil_curve_S_W
FTILE:      E = sigil_curve_N_E
FTILE:      F = sigil_curve_N_W
FTILE:      G = sigil_straight_E_SW
FTILE:      H = sigil_straight_W_SE
FTILE:      I = sigil_straight_S_NE
FTILE:      J = sigil_straight_S_NW
FTILE:      L = sigil_straight_N_SE
FTILE:      M = sigil_straight_N_SW
FTILE:      N = sigil_straight_E_NW
FTILE:      P = sigil_straight_W_NE
RTILE:      x = wall_undead
SUBST:      u = 3
MARKER:     f = lua:fog_machine { \
                pow_max = 10, delay_min = 10, delay_max = 40, \
                size = 2, size_buildup_amnt = 5, \
                size_buildup_time = 25, cloud_type = "flame", \
                excl_rad = -1 }
KMONS:      2 = w:1 Lorocyproca / hell beast / reaper /\
            w:20 blizzard demon / w:20 sun demon / hellion
KMONS:      3 = ice devil / iron devil / soul eater / neqoxec /\
            w:5 tormentor / ynoxinul
KMONS:      4 = kobold demonologist / deep elf demonologist
KMONS:      5 = balrug / green death / cacodemon
{{
wizname = crawl.make_name()

kmons     ("1 = patrolling Hellbinder dbname:Hellbinder name:헬바인더 name_replace")
}}
MARKER:     1 = lua:MonPropsMarker:new {description="강력한 마법사로, 마력장이 마법사의 주위를 휘감고 있다. 당신이 지금까지 들어본 " \  
            .. "것 중에 가장 불경한 주문을 웅얼거리고 있다." \  
            .. ".\n"}  
KITEM:      $ = gold / w:1 scroll of torment / w:1 scroll of summoning
KITEM:      i = rod of demonology / ring of fire / ring of protection from fire /\
            amulet of conservation / scroll of torment / scroll of summoning /\
            demon blade w:1 / demon whip w:1 / demon trident w:1 / gold / wand of fire /\
            wand of draining / staff of summoning / any book w:30 / any good_item w:15
KITEM:      B = randbook disc:summoning
NSUBST:     i = 2:B / *:i
:           set_random_mon_list([[crimson imp / shadow imp / lemure / iron imp / quasit / white imp / ]]
:                        .. [[ufetubus / red devil / rotting devil / orange demon / ]])
:           wizlab_setup(_G, "헬바인더의 회랑") 
:           set_feature_name("floor", "뜨거운 바닥")
:           set_feature_name("stone_wall", "검은 벽")
:           set_feature_name("rock_wall", "조각된 암벽")
epilogue{{
            wizlab_milestone(_G, "헬바인더의 회랑") 
}}
MAP
cccccccccccccccccccccccccccccccccccccccccccccc
ccccccc.cccccc...................cccccc.cccccc
ccccc.....cccc.cc+ccccc+ccccc+cc.cccc.....cccc
cccc..G-H..ccc.c...cccc.cccc...c.ccc...o...ccc
ccc../...\..cc.+.A.ccc...ccc.<.+.cc..|...../cc
ccc.I...3.J.cc.c...cc.....cc...c.cc.-O-.3./.cc
cc..|..u..|....ccccc..c.c..ccccc.....|.3./...c
cc..|.3Y..|....cccc..cc.cc..cccc......3./....c
ccc.L.....M.cc.ccc.....4.....ccc.cc....K----cc
ccc..\.../..cc.cc..cccc.cccc..cc.cc.../.....cc
cccc..N-P..ccc.+...............+.ccc./.....ccc
ccccc.....cccc.ccccccccccccccccc.cccc.....cccc
ccccccc.cccccc...................cccccc.cccccc
ccccccc.cccccc.cccccccc.cccccccc.cccccc.cccccc
ccccccc.ccccccc.cccccc.c.cccccc.ccccccc.cccccc
ccccccc.cccccccc.cccc.ccc.cccc.cccccccc.cccccc
ccccccc.ccccccccc.cc.ccccc.cc.ccccccccc.cccccc
ccccccc.cccccccccc..ccccccc..cccccccccc.cccccc
ccccccc.cccc.5...................5.cccc.cccccc
ccccccc.cccc..ccccccccc.ccccccccc..cccc.cccccc
ccccccc.ccccc..cccc$$$c.c$$$cccc..ccccc.cccccc
ccccccc.cccccc..ccc$$$+.+$$$ccc..cccccc.cccccc
ccccccc.cc.cccc..cc$$$c.c$$$cc..cccc.cc.cccccc
ccccccc......ccc..ccccc.ccccc..ccc..Z...cccccc
ccccccc...o...ccc..cccc.cccc..ccc../..O.cccccc
cccccc..\.../..ccc..ccc.ccc..ccc../..OTO.ccccc
cccccc...\2/...cccc..cc.cc..cccc./2222U..ccccc
ccccc..\2CXD2/..cccc.......cccc./.........cccc
cccccc..\|2|/...ccccc.\4/.cccccS--------Zccccc
cccccc...Q.R....ccccc..Y..ccccc......../.ccccc
ccccccc..ETF....ccccc..|..ccccc......./.cccccc
cccccccc........ccccc.....ccccc......S.ccccccc
cccccccccc......cccc..ccc..cccc......ccccccccc
ccccccccccc.....ccc..ccccc..ccc.....cccccccccc
cccccccccccc.....c...ccccc...c.....ccccccccccc
cccccccccccc..\./..cc.ccc.cc..\./..ccccccccccc
ccccccccccccc..V..cccc.c.cccc..W..cccccccccccc
ccccccccccccc./...cccccfccccc...\.cccccccccccc
cccccccccccccc.....ccc.c.ccc.....ccccccccccccc
ccccccccccccccc.....c.ccc.c.....cccccccccccccc
ccccccccccccccccc.....ccc.....cccccccccccccccc
ccccccccccccccccccc.........cccccccccccccccccc
ccccccccccccccccccccc.....cccccccccccccccccccc
ccccccccccccccccccccccc+cccccccccccccccccccccc
ccccccccccccccccccccc.....cccccccccccccccccccc
ccccccccccccccccccc...G"H...cccccccccccccccccc
ccccccccccccccccc...GP...NH...cccccccccccccccc
cccccccccccccccc..G[.......]H..ccccccccccccccc
ccccccccccccccc../..N.....P..\..cccccccccccccc
ccccccccccccccc.I.............J.cccccccccccccc
cccccccccccccc..U.....G.H.....U..ccccccccccccc
cccccccccccccc.I.J.../...\...I.J.ccccccccccccc
ccccccccccccc..|..../..1..\....|..cccccccccccc
ccccccccccccc..|.../\.'''./\...|..cccccccccccc
ccccccccccccc..|../xx\'_'/xx\..|..cccccccccccc
cccccccccccc...L./..xxN'Pxx..\.M...ccccccccccc
cccccccccccci...W..G.xxxxx.H..V...iccccccccccc
ccccccccccccii...NP.........NP...iiccccccccccc
cccccccccccciii.................iiiccccccccccc
cccccccccccccccccccccccccccccccccccccccccccccc
ENDMAP

###############################################################################
# Incomplete and TODO work area.
#
# See git history for previous versions of Ozocubu, Alistair.
#
# March 2013 note: there isn't the greatest of interest in making
# the majority of these potential portals, as nice as that would be,
# and the portal is effectively "finished".
# More effort should be put into editing above vaults anyway.
#
# PLANNING AREA:
#
# From spells:
#
# Alistair's Brewery: mutated wizards, mephitic cloud generators,
#    cupboards full of potions, confused monsters.
# Lee's Rapid Deconstructor: golems and "monk" or otherwise unarmed creatures,
#    exploding features and exploding corpses. It's a timed vault, so you have
#    to get to the loot quickly and avoid the hordes of monsters.
# Lehudib's Crystal Spire:  crystal walls and crystal golems, crystal monsters,
#    spear throwers (statues with crystal spear), possibly the unrand as the
#    loot.
# Maxwell's Forge: Maxwell employs a bunch of angels to churn out weapons;
#    stashes of hammers, flame clouds, other "smithy" styled themes, with black
#    smoke.
# Olgreb's Toxic Laboratory: poisonous clouds, a toxic radiance effect,
#    poisonous monsters, possibly even a "poison" effect that bypasses rPois,
#    potions of poison and potions dropped converting into potions of poison.
# Ozocubu's Refrigerator: simulacra, refrigeration effect, a solid stone room
#    inside of an "ice cave", freezing clouds.
#
# From unrands:
#
# Cekugob's Oubliette: a randomised maze (though not a labyrinth) with skeletal
#    guards, and with occasionally hidden. Cekugob's amulet as the loot,
#    possibly.
# Botono's Bayou: A bay-like area with small huts, wizards that cast negative
#    related spells, shadow dragons, rebranded swamp worms, wraiths, spectrals.
# Ukta's Hut: phantoms, other "ghosts", ogre mages and ogres, druidic style,
#    wooden walls set in a forest (more "earthy" than Eringya's Garden)
# The Alchemist's Tower: gold turned into things, things turned into gold.
#
# Other mages that could use representation, not yet expounded upon:
# Borgnjor, Leda, Doom Knight, Octopus King, The Captain, Zhor.
#
